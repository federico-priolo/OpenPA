000010  INIZIO-WEB.
000020
000030      CALL  "COBW3_INIT"      USING  COBW3 .
000040
000050* SET the CONTENT-TYPE to HTML
000060      SET COBW3-CONTENT-TYPE-HTML TO TRUE .
000070
000080* COMMENTARE PER DISATTIVARE IL DEBUG NATIVO
000090*      MOVE "1"             TO COBW3-DMODE.
000100
000110  EX-INIZIO-WEB.
000120        EXIT.
000130
000140
000150  FINE-WEB.
000160
000170      CALL  "COBW3_FREE"       USING  COBW3 .
000180
000190  EX-FINE-WEB.
000200        EXIT.
000210
000220  LETTURA-WEB.
000230
000240         IF NOT OPENED-WEB
000250          OPEN INPUT ARKWEB
000260           SET OPENED-WEB    TO TRUE
000270           MOVE "N"          TO EOF-WEB
000280           IF STATUS-WEB NOT = "00"
000290           STRING " ERRORE APERTURA " FILE-WEB
000300            DELIMITED BY SIZE INTO MESSAGGIO
000310            PERFORM VIS-MESS  THRU EX-VIS-MESS
000320             GO TO END-LETTURA-WEB.
000330
000340          READ ARKWEB NEXT RECORD AT END
000350           GO TO END-LETTURA-WEB.
000360
000370          IF DATI-WEB = SPACES GO TO LETTURA-WEB.
000380
000400
000410          PERFORM LINE-WEB    THRU EX-LINE-WEB.
000420
000430
000440          GO TO EX-LETTURA-WEB.
000450
000460  END-LETTURA-WEB.
000470
000480          SET EOF-FILE-WEB  TO TRUE.
000490          MOVE SPACE        TO FLAG-WEB.
000500
000510          CLOSE ARKWEB.
000520
000530  EX-LETTURA-WEB.
000540          EXIT.
000550
000560
000570  VIS-MESS.
000580
000590          MOVE MESSAGGIO      TO VALUE-WEB.
000600          MOVE "MK-MESSAGE"   TO FIELD-WEB.
000610          PERFORM METTI-WEB   THRU EX-METTI-WEB.
000620
000630          MOVE "SEGNALAZIONE" TO VALUE-WEB.
000640          MOVE "MK-TITLE"     TO FIELD-WEB.
000650          PERFORM METTI-WEB   THRU EX-METTI-WEB.
000660
000670          MOVE SECTION-WEB    TO VALUE-WEB.
000680          MOVE "MK-KEY"       TO FIELD-WEB.
000690          PERFORM METTI-WEB   THRU EX-METTI-WEB.
000700
000710
000720			MOVE "GOBACK"		TO NOME-VIEW.
000730			PERFORM LEGGO-VIEW	THRU EX-LEGGO-VIEW.
000740			IF ESITO-NOK MOVE SPACES TO STRINGA-VIEW.
000750
000760          MOVE STRINGA-VIEW   TO VALUE-WEB.
000770          MOVE "MK-BACK"      TO FIELD-WEB.
000780          PERFORM METTI-WEB   THRU EX-METTI-WEB.
000790
000800          MOVE "TEMPLATE/MESSAGE.HTM"  TO PAGE-WEB.
000810          PERFORM SHOW-WEB    THRU EX-SHOW-WEB.
000820          MOVE SPACES         TO MESSAGGIO.
000830
000840  EX-VIS-MESS.
000850          EXIT.
000860
000870  VIS-MENU.
000880
000890          MOVE MESSAGGIO      TO VALUE-WEB.
000900          MOVE "MK-MESSAGE"   TO FIELD-WEB.
000910          PERFORM METTI-WEB   THRU EX-METTI-WEB.
000920
000930          MOVE "SEGNALAZIONE" TO VALUE-WEB.
000940          MOVE "MK-TITLE"     TO FIELD-WEB.
000950          PERFORM METTI-WEB   THRU EX-METTI-WEB.
000960
000970          MOVE SECTION-WEB    TO VALUE-WEB.
000980          MOVE "MK-KEY"       TO FIELD-WEB.
000990          PERFORM METTI-WEB   THRU EX-METTI-WEB.
001000
001010
001020          MOVE "TEMPLATE/GOMENU.HTM"  TO PAGE-WEB.
001030          PERFORM SHOW-WEB    THRU EX-SHOW-WEB.
001040
001050  EX-VIS-MENU.
001060          EXIT.
001070
001080  VIS-LOGIN.
001090
001100          move spaces to messaggio
001110
001120          string "Utente non presente o sessione di lavoro"
001130           " scaduta  - e' necessario effettuare "
001140           " la login" DELIMITED BY SIZE INTO messaggio
001150          MOVE MESSAGGIO      TO VALUE-WEB.
001160          MOVE "MK-MESSAGE"   TO FIELD-WEB.
001170          PERFORM METTI-WEB   THRU EX-METTI-WEB.
001180
001190          MOVE "SEGNALAZIONE" TO VALUE-WEB.
001200          MOVE "MK-TITLE"     TO FIELD-WEB.
001210          PERFORM METTI-WEB   THRU EX-METTI-WEB.
001220
001230
001240
001250          MOVE "TEMPLATE/GOLOGIN.HTM"    TO PAGE-WEB.
001260          PERFORM SHOW-WEB    THRU EX-SHOW-WEB.
001270
001280  EX-VIS-LOGIN.
001290          EXIT.
001300
001310
001320  METTI-WEB.
001330
001340          MOVE FIELD-WEB    TO COBW3-CNV-NAME.
001350          MOVE VALUE-WEB    TO COBW3-CNV-VALUE.
001360          MOVE ZEROS        TO COBW3-CNV-NAME-LENGTH.
001370          MOVE ZEROS        TO COBW3-CNV-VALUE-LENGTH.
001380          CALL "COBW3_CNV_SET" USING COBW3.
001390          MOVE SPACES       TO COBW3-CNV-VALUE.
001400
001410  EX-METTI-WEB.
001420          EXIT.
001430
001440
001450  SHOW-WEB.
001460
001470          MOVE PAGE-WEB     TO COBW3-HTML-FILENAME.
001480          CALL "COBW3_PUT_HTML" USING COBW3.
001490
001500  EX-SHOW-WEB.
001510          EXIT.
001520
001530  LINE-WEB.
001540
001550          IF DATI-WEB > SPACES
001560          MOVE DATI-WEB       TO     COBW3-PUT-STRING
001570          MOVE ZEROS          TO     COBW3-PUT-STRING-LENGTH
001580          CALL  "COBW3_PUT_TEXT"   USING  COBW3
001590
001600          MOVE SPACES             TO DATI-WEB.
001610
001620  EX-LINE-WEB.
001630          EXIT.
001640
001650
001660  READ-WEB.
001670
001680        IF INDEX-WEB NOT > 1
001690          MOVE 1          TO  COBW3-NUMBER
001700         ELSE
001710          MOVE INDEX-WEB TO  COBW3-NUMBER.
001720
001730        MOVE FIELD-WEB   TO  COBW3-SEARCH-DATA
001740        CALL "COBW3_NAME" USING COBW3
001750        MOVE COBW3-GET-DATA (1:COBW3-GET-LENGTH) TO VALUE-WEB.
001760
001770        IF  COBW3-SEARCH-FLAG-EXIST
001780         SET ON-WEB TO TRUE
001790         ELSE
001800         SET OFF-WEB TO TRUE.
001810
001820         IF VALUE-WEB(1:2) NUMERIC
001830          AND VALUE-WEB(3:1) = "/"
001840           AND VALUE-WEB(4:2) NUMERIC
001850            AND VALUE-WEB(6:1) = "/"
001860             AND VALUE-WEB(7:4) NUMERIC
001870
001880             MOVE VALUE-WEB(1:2) TO GG-WEB
001890             MOVE VALUE-WEB(4:2) TO MM-WEB
001900             MOVE VALUE-WEB(7:4) TO AA-WEB
001910              ELSE
001920              INITIALIZE DATA-WEB.
001930
001940
001950          PERFORM ALFA-TO-NUM-WEB THRU EX-ALFA-TO-NUM-WEB.
001960
001970
001980  EX-READ-WEB.
001990          EXIT.
002000
002010  MAKE-WEB.
002020
002030          MOVE PAGE-WEB TO FILE-WEB.
002040          MOVE "N"      TO EOF-WEB.
002050          PERFORM UNTIL EOF-FILE-WEB
002060          PERFORM LETTURA-WEB THRU EX-LETTURA-WEB
002070          END-PERFORM.
002080
002090  EX-MAKE-WEB.
002100          EXIT.
002110
002120
002130  ESEGUI-WEB.
002140*
002150**** mettere il comando in COBW3-SYSTEMINFO.
002160*
002170          CALL "COBW3_SYSTEM" USING COBW3.
002180          MOVE SPACES       TO COBW3-SYSTEMINFO.
002190
002200			IF COBW3-STATUS = 1
002210			STRING "Errore esecuzione " COBW3-SYSTEMINFO
002220			 DELIMITED BY SIZE INTO MESSAGGIO
002230			  PERFORM VIS-MESS THRU EX-VIS-MESS.
002240
002250  EX-ESEGUI-WEB.
002260          EXIT.
002270
002280
002290  SHA-WEB.
002300
002310			MOVE SPACES TO COBW3-SYSTEMINFO.
002320
002330          STRING 'openSSL passwd -salt x ' DELIMITED BY SIZE
002340          FIELD-WEB DELIMITED BY "   "
002350          " > /temp/SHA" DELIMITED BY SIZE 
002360           SECTION-WEB ".log" 
002370            DELIMITED BY SIZE INTO COBW3-SYSTEMINFO.
002380			 
002390          PERFORM ESEGUI-WEB 	  THRU EX-ESEGUI-WEB.
002400
002410			CALL "SAVESHA3" USING SECTION-WEB
002420			 RETURNING VALUE-WEB.
002430
002440			CANCEL "SAVESHA3".
002450
002460
002470  EX-SHA-WEB.
002480			EXIT.
002490
002500  CALL-WEB.
002510
002520          CALL "POWEROPENSHEET" USING PROGRAMMA-WEB
002530                                      LIBRERIA-WEB
002540                          RETURNING RITORNO-WEB.
002550
002560
002570  EX-CALL-WEB.
002580          EXIT.
002590
002600  REPLACE-STANDARD-WEB.
002610*
002620* qui processa un include di file <cobol include=template.hmt></cobol>
002630*
002640          MOVE SPACES     TO FILE-LIB
002650
002660          MOVE ZEROS      TO IND1-WEB IND-WEB.
002670
002680          INSPECT FUNCTION UPPER-CASE(DATI-WEB) TALLYING IND1-WEB 
002690              FOR ALL "<COBOL INCLUDE="
002700
002710          MOVE SPACES     TO FILE-LIB
002720
002730          IF IND1-WEB  = 1
002740          MOVE SPACES     TO NOME-VIEW
002750
002760          PERFORM VARYING IND-WEB FROM 1 BY 1 
002770          UNTIL IND-WEB > LENGTH OF DATI-WEB 
002780           OR FUNCTION UPPER-CASE(DATI-WEB(IND-WEB:8)) = "INCLUDE="
002790            CONTINUE
002800          END-PERFORM
002810
002820          IF FUNCTION UPPER-CASE(DATI-WEB(IND-WEB:8)) = "INCLUDE="
002830          ADD 8       TO IND-WEB          
002840          MOVE 1      TO IND1-WEB
002850          PERFORM VARYING IND-WEB FROM IND-WEB BY 1 
002860           UNTIL IND-WEB > LENGTH OF DATI-WEB                                 
002870            OR DATI-WEB(IND-WEB:1) = ">"
002880              MOVE DATI-WEB(IND-WEB:1) TO FILE-LIB(IND1-WEB:1)
002890              ADD 1 TO IND1-WEB
002900          END-PERFORM
002910
002920          PERFORM TRATTA-LIB THRU EX-TRATTA-LIB.
002930
002940
002950  CICLO-COBOL-WEB. 
002960*
002970*         <cobol>NAME</cobol>  --> return the variable 
002980*
002990*         <cobol argument=" ">NAME</cobol>  --> return the variable with arguments
003000*
003010
003020          MOVE ZEROS TO IND-WEB.
003030
003040          INSPECT FUNCTION UPPER-CASE(DATI-WEB) 
003050             TALLYING IND-WEB FOR ALL "<COBOL"
003060          INSPECT FUNCTION UPPER-CASE(DATI-WEB) 
003070             TALLYING IND-WEB FOR ALL "</COBOL>"
003080
003090**** COPPIE DI <cobol DEFINISCONO UNA VARIABILE
003100
003110          IF IND-WEB  < 2 GO TO EX-REPLACE-STANDARD-WEB.
003120
003130          MOVE SPACES TO NOME-VIEW.
003140
003150          PERFORM VARYING IND-WEB FROM 1 BY 1 UNTIL
003160          IND-WEB > LENGTH OF DATI-WEB 
003170           OR FUNCTION UPPER-CASE(DATI-WEB(IND-WEB:6)) = "<COBOL"
003180           CONTINUE
003190          END-PERFORM.
003200
003210**** NB. la variabile dovra iniziare da INIZIO-WEB..  <COBOL>NAME</COBOL> -->'VALUE'
003220
003230         MOVE IND-WEB     TO POS-WEB
003240*        
003250*    se siamo in presenza di <cobol arguments="values...">NAME </cobol>
003260*    vuol dire che c'e bisogno di leggere un template in "template=" e gli argmenti 
003270*    vengono inseriti nella lettura
003280*
003290          IF FUNCTION UPPER-CASE(DATI-WEB(IND-WEB:7)) = "<COBOL>"
003300          ADD 7           TO IND-WEB
003310
003320          ELSE
003330          
003340          MOVE  DATI-WEB(IND-WEB:)                    TO ARGUMENT
003350
003360          PERFORM VARYING IND-WEB FROM 1 BY 1 
003370          UNTIL IND-WEB > LENGTH OF DATI-WEB 
003380           OR FUNCTION UPPER-CASE(DATI-WEB(IND-WEB:9)) = "TEMPLATE="
003390            CONTINUE
003400          END-PERFORM
003410
003420          IF FUNCTION UPPER-CASE(DATI-WEB(IND-WEB:9)) = "TEMPLATE="
003430          ADD 9       TO IND-WEB          
003440          move ""    TO FILE-LIB
003450          MOVE 10     TO IND1-WEB
003460          PERFORM VARYING IND-WEB FROM IND-WEB BY 1 
003470           UNTIL IND-WEB > LENGTH OF DATI-WEB                                 
003480            OR DATI-WEB(IND-WEB:1) = " "
003490              MOVE DATI-WEB(IND-WEB:1) TO FILE-LIB(IND1-WEB:1)
003500              ADD 1 TO IND1-WEB
003510          END-PERFORM
003520
003530          PERFORM TRATTA-LIB THRU EX-TRATTA-LIB
003540         
003550          MOVE SPACES         TO DATI-WEB
003560          
003570          GO TO CICLO-COBOL-WEB.
003580*          
003590** IND-WEB ora punta alla stringa <cobol>STRINGA</cobol>
003600*
003610          MOVE  IND-WEB   TO IND1-WEB.
003620
003630          MOVE ZERO TO SIZE-WEB.
003640
003650          PERFORM VARYING IND1-WEB FROM IND1-WEB BY 1 UNTIL
003660           IND1-WEB > LENGTH OF DATI-WEB 
003670            OR FUNCTION UPPER-CASE(DATI-WEB(IND1-WEB:8)) = "</COBOL>"
003680
003690            ADD 1    TO SIZE-WEB
003700
003710          END-PERFORM.
003720
003730** SIZE-WEB ha la dimensione della stringa <cobol>STRINGA</cobol>
003740*                                                 {LENGTH}
003750***** SALVA DA DOPO </COBOL>------ IN BUFFER
003760
003770			COMPUTE IND2-WEB = IND1-WEB + 8.
003780
003790          MOVE DATI-WEB(IND2-WEB:) TO BUFFER.
003800
003810			
003820          MOVE DATI-WEB(IND-WEB:SIZE-WEB)  TO NOME-VIEW
003830
003840          PERFORM LEGGO-VIEW THRU EX-LEGGO-VIEW.
003850
003860          UNLOCK ARKWEB RECORD.
003870
003880          IF ESITO-NOK MOVE  SPACES TO EDIT-VIEW.
003890
003900          MOVE EDIT-VIEW  TO DATI-WEB(POS-WEB:)
003910
003920          IF DATI-WEB = SPACES
003930           MOVE BUFFER TO DATI-WEB
003940             GO TO EX-REPLACE-STANDARD-WEB.
003950
003960          PERFORM VARYING IND-WEB FROM LENGTH OF DATI-WEB
003970            BY -1 UNTIL DATI-WEB(IND-WEB:1) > SPACES
003980            CONTINUE
003990          END-PERFORM.
004000
004010          ADD 1 TO IND-WEB.
004020
004030          MOVE "/DIVISORE/" TO DATI-WEB(IND-WEB:).
004040
004050          MOVE SPACES     TO BUFFER1.
004060
004070          STRING DATI-WEB DELIMITED BY "/DIVISORE/"
004080              BUFFER DELIMITED BY SIZE INTO BUFFER1.
004090
004100          MOVE BUFFER1    TO  DATI-WEB.
004110
004120          GO TO CICLO-COBOL-WEB.
004130
004140
004150  EX-REPLACE-STANDARD-WEB.
004160          EXIT.
004170
004180
004190
004200  LETTURA-LIB.
004210
004220         IF NOT OPENED-LIB
004230          OPEN INPUT ARKLIB
004240           SET OPENED-LIB    TO TRUE
004250           MOVE "N"          TO EOF-LIB
004260           IF STATUS-LIB NOT = "00"
004270           STRING " ERRORE APERTURA " FILE-LIB
004280            DELIMITED BY SIZE INTO MESSAGGIO
004290            PERFORM VIS-MESS  THRU EX-VIS-MESS
004300             GO TO END-LETTURA-LIB.
004310
004320          READ ARKLIB NEXT RECORD AT END
004330           GO TO END-LETTURA-LIB.
004340
004350          IF DATI-LIB = SPACES GO TO LETTURA-LIB.
004360
004370*
004380*     un file lib non puo avere <cobol arguents=.....> </cobol>
004390* 
004400*     ma solo variabili <cobol>NAME</name> oppure variabili nella forma {VALUE} estratte da ARUGMENT
004410*
004420*
004430
004440
004450          MOVE ZEROS          TO IND-LIB.
004460          
004470          INSPECT DATI-LIB TALLYING IND-LIB FOR ALL "{"
004480          INSPECT DATI-LIB TALLYING IND-LIB FOR ALL "}"
004490          
004500** processiamo solo 6 parametri nella riga nella forma {NAME}
004510
004520          IF IND-LIB = 2 OR 4 OR 6 OR 8 OR 10 OR 12 
004530          PERFORM VARYING IND-LIB FROM 1 BY 1 
004540           UNTIL DATI-LIB(IND-LIB:20) = SPACES OR IND-LIB > LENGTH OF DATI-LIB
004550           IF DATI-LIB(IND-LIB:1) = "{"
004560            PERFORM ARGOMENTO-LIB  THRU EX-ARGOMENTO-LIB
004570             
004580           END-IF
004590
004600          END-PERFORM.
004610
004620          MOVE ZEROS          TO IND-LIB.
004630          
004640          INSPECT DATI-LIB TALLYING IND-LIB FOR ALL "{"
004650          INSPECT DATI-LIB TALLYING IND-LIB FOR ALL "}"
004660          
004670          IF IND-LIB NOT = ZEROS 
004680*
004690* rimuoviamo tutti i tag ancora presenti con {VALUE} in quanto non referenziati nella call <cobol...></cobol>
004700*
004710           PERFORM VARYING IND-LIB  FROM  LENGTH OF DATI-LIB BY -1 
004720              UNTIL IND-LIB = ZEROS           
004730                OR DATI-LIB(IND-LIB:2) = '}"'
004740                  CONTINUE
004750           END-PERFORM
004760           ADD 1 TO IND-LIB
004770           PERFORM  VARYING IND-LIB FROM IND-LIB BY -1 
004780                  UNTIL DATI-LIB(IND-LIB:1) = "{"
004790                  MOVE SPACES TO DATI-LIB(IND-LIB:1)
004800           END-PERFORM
004810
004820           PERFORM  VARYING IND-LIB FROM IND-LIB BY -1 
004830                  UNTIL DATI-LIB(IND-LIB:1) = " "
004840                  MOVE SPACES TO DATI-LIB(IND-LIB:1)
004850           END-PERFORM.
004860
004870          MOVE ZEROS          TO IND-LIB.
004880
004890          INSPECT FUNCTION UPPER-CASE(DATI-LIB) 
004900             TALLYING IND-LIB FOR ALL "<COBOL>"
004910          INSPECT FUNCTION UPPER-CASE(DATI-LIB) 
004920             TALLYING IND-LIB FOR ALL "</COBOL>"
004930
004940          IF IND-LIB  = 2
004950           MOVE DATI-LIB      TO DATI-WEB
004960			  PERFORM REPLACE-STANDARD-WEB THRU EX-REPLACE-STANDARD-WEB
004970            PERFORM LINE-WEB    THRU EX-LINE-WEB
004980				GO TO LETTURA-LIB.
004990
005000          
005010
005020          MOVE DATI-LIB       TO DATI-WEB.
005030
005040          PERFORM LINE-WEB    THRU EX-LINE-WEB.
005050
005060
005070          GO TO LETTURA-LIB.
005080
005090  END-LETTURA-LIB.
005100
005110          SET EOF-FILE-LIB  TO TRUE.
005120          MOVE SPACE        TO FLAG-LIB.
005130
005140          CLOSE ARKLIB.
005150
005160  EX-LETTURA-LIB.
005170          EXIT.
005180 
005190  ARGOMENTO-LIB.
005200  
005210          MOVE IND-LIB    TO IND2-LIB
005220
005230          ADD 1           TO IND-LIB
005240          MOVE ZEROS      TO SIZE-WEB
005250
005260
005270          PERFORM VARYING IND1-LIB FROM IND-LIB BY 1 UNTIL 
005280            IND1-LIB > LENGTH OF DATI-LIB    
005290            OR DATI-LIB(IND1-LIB:1) = "}"
005300              ADD 1  TO SIZE-WEB
005310              
005320           END-PERFORM
005330           
005340**** salva tutto quello c'e dopo } 
005350
005360           MOVE DATI-LIB(IND1-LIB + 1:)    TO BUFFER1
005370           MOVE SPACES                     TO BUFFER
005380           
005390           MOVE FUNCTION UPPER-CASE(DATI-LIB(IND-LIB:SIZE-WEB)) TO VALUE-WEB
005400
005410**** VALUE-WEB ora contiene tutto il nome della {VARIABILE} e la cerca in ARGUMENT
005420
005430           
005440           PERFORM VARYING IND2-WEB FROM 1 BY 1 UNTIL IND2-WEB > LENGTH OF ARGUMENT
005450
005460              OR FUNCTION UPPER-CASE(ARGUMENT (IND2-WEB:SIZE-WEB))
005470                EQUAL FUNCTION UPPER-CASE(VALUE-WEB(1:SIZE-WEB))
005480              
005490              CONTINUE
005500              
005510           END-PERFORM.
005520
005530          
005540          
005550*          
005560* ha trovato {TITLE} E title="qualcosa" ...deve sostituire "qualcosa" dove c'era {TITLE}
005570*                ^        ^   
005580*            DATI-LIB   ARGUMENT
005590
005600           IF IND2-WEB   NOT > LENGTH OF ARGUMENT 
005610
005620              IF FUNCTION UPPER-CASE(ARGUMENT (IND2-WEB:SIZE-WEB))
005630                EQUAL FUNCTION UPPER-CASE(VALUE-WEB(1:SIZE-WEB))
005640
005650                 ADD SIZE-WEB     TO IND2-WEB
005660                
005670                 PERFORM VARYING IND2-WEB FROM IND2-WEB BY 1 
005680                      UNTIL IND2-WEB > LENGTH OF ARGUMENT
005690                      OR ARGUMENT(IND2-WEB:2) = '="'
005700                      CONTINUE
005710                      
005720                 END-PERFORM
005730                 
005740                 ADD 2            TO IND2-WEB
005750                 
005760                 MOVE IND2-WEB    TO POS-WEB
005770                 MOVE ZEROS       TO SIZE-WEB
005780                 PERFORM VARYING IND2-WEB FROM IND2-WEB BY 1 
005790                      UNTIL IND2-WEB > LENGTH OF ARGUMENT
005800                      OR ARGUMENT(IND2-WEB:1) = '"'
005810                      ADD 1       TO SIZE-WEB
005820                 END-PERFORM
005830
005840                MOVE SPACES       TO BUFFER
005850                
005860                 STRING ARGUMENT(POS-WEB:SIZE-WEB) 
005870                         "/DIVISORE/" DELIMITED BY SIZE INTO BUFFER.
005880
005890
005900              IF FUNCTION UPPER-CASE(VALUE-WEB) = "SELF"
005910                MOVE SPACES               TO BUFFER       
005920                 PERFORM VARYING IND2-WEB FROM IND2-LIB BY 1 
005930                      UNTIL IND2-WEB  > LENGTH OF ARGUMENT
005940                      OR ARGUMENT(IND2-WEB:1) = ">"
005950                      CONTINUE
005960                      END-PERFORM
005970                      
005980                      ADD 1       TO IND2-WEB
005990                      MOVE ZEROS  TO SIZE-WEB
006000                      MOVE SPACES TO BUFFER
006010                      PERFORM  VARYING IND2-WEB FROM IND2-WEB BY 1
006020                      UNTIL IND2-WEB  > LENGTH OF ARGUMENT
006030                      OR FUNCTION UPPER-CASE(ARGUMENT(IND2-WEB:8)) = "</COBOL>"
006040                      ADD 1       TO SIZE-WEB
006050                      CONTINUE
006060                      END-PERFORM
006070                      SUBTRACT SIZE-WEB FROM IND2-WEB
006080                      STRING ARGUMENT(IND2-WEB:SIZE-WEB) 
006090                      "/DIVISORE/" DELIMITED BY SIZE INTO BUFFER.
006100                      
006110              
006120              MOVE SPACES TO DATI-WEB.
006130
006140              if BUFFER = SPACES          
006150**** BUFFER NON valorizzato: c'e un {VALUE} NON sostitutito si esce... dopo sopra lo leviamo
006160                   GO TO EX-ARGOMENTO-LIB.
006170              
006180  
006190  
006200       
006210
006220*  IND2-LIB = -> {  DOBBIAMO prendere la dalla sua sinistra a ritroso
006230
006240              SUBTRACT 1 FROM IND2-LIB.
006250
006260              STRING DATI-LIB(1:IND2-LIB)
006270                BUFFER DELIMITED BY "/DIVISORE/"
006280                  BUFFER1 DELIMITED BY "             "
006290                  INTO DATI-WEB.
006300            
006310              MOVE DATI-WEB   TO DATI-LIB.
006320
006330        
006340
006350  EX-ARGOMENTO-LIB.
006360          EXIT.
006370  
006380  TRATTA-LIB.
006390
006400          MOVE "N"      TO EOF-LIB.
006410          PERFORM UNTIL EOF-FILE-LIB
006420          PERFORM LETTURA-LIB THRU EX-LETTURA-LIB
006430          END-PERFORM.
006440
006450  EX-TRATTA-LIB.
006460          EXIT.
006470
006480
006490  ALFA-TO-NUM-WEB.
006500
006510
006520          MOVE ALL ZEROS          TO TAB-NUMERO-WEB.
006530
006540          MOVE 18                 TO IND3-WEB
006550          PERFORM VARYING IND2-WEB FROM 18 BY -1 UNTIL IND2-WEB = ZERO
006560
006570          IF VALUE-WEB(IND2-WEB:1) NUMERIC
006580          MOVE  VALUE-WEB(IND2-WEB:1)   TO TAB-NUMERO-WEB(IND3-WEB:1)
006590          SUBTRACT 1 FROM IND3-WEB
006600          END-IF
006610
006620          END-PERFORM.
006630
006640          MOVE TAB-NUMERO-WEB     TO NUMERO-WEB.
006650
006660
006670  EX-ALFA-TO-NUM-WEB.
006680          EXIT.
