000010 @OPTIONS ALPHAL(WORD)
000020*
000030* Copyright (C) 2022 Federico Priolo TP ONE SRL federico.priolo@tp-one.it
000040*
000050* This program is free software; you can redistribute it and/or modify
000060* it under the terms of the GNU General Public License as published by
000070* the Free Software Foundation; either version 2, or (at your option)
000080* any later version.
000090*
000100* This program is distributed in the hope that it will be useful,
000110* but WITHOUT ANY WARRANTY; without even the implied warranty of
000120* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
000130* GNU General Public License for more details.
000140*
000150* You should have received a copy of the GNU General Public License
000160* along with this software; see the file COPYING.  If not, write to
000170* the Free Software Foundation, 51 Franklin Street, Fifth Floor
000180* Boston, MA 02110-1301 USA
000190*
000200
000210*
000220 identification division.
000230 PROGRAM-ID. MAKEFORM.
000240 AUTHOR. FEDERICO PRIOLO AGOSTO 2022
000250
000260*
000270* Copyright (C) 2010-2022 Federico Priolo TP ONE SRL federico.priolo@tp-one.it
000280*
000290* This program is free software; you can redistribute it and/or modify
000300* it under the terms of the GNU General Public License as published by
000310* the Free Software Foundation; either version 2, or (at your option)
000320* any later version.
000330*
000340* This program is distributed in the hope that it will be useful,
000350* but WITHOUT ANY WARRANTY; without even the implied warranty of
000360* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
000370* GNU General Public License for more details.
000380*
000390* You should have received a copy of the GNU General Public License
000400* along with this software; see the file COPYING.  If not, write to
000410* the Free Software Foundation, 51 Franklin Street, Fifth Floor
000420* Boston, MA 02110-1301 USA
000430*
000440 ENVIRONMENT DIVISION.
000450 CONFIGURATION    SECTION.
000460
000470          COPY "SPECIAL.CBL".
000480
000490 INPUT-OUTPUT SECTION.
000500 FILE-CONTROL.
000510
000520*
000530			COPY "SELWEB.CBL".
000540			COPY "SELMENU.CBL".
000550			COPY "SELABI.CBL".
000560			COPY "SELVIEW.CBL".
000570			COPY "SELUTEN.CBL".
000580
000590 DATA DIVISION.
000600 FILE SECTION.
000610
000620
000630			COPY "FDEWEB.CBL".
000640			COPY "FDEABI.CBL".
000650			COPY "FDEMENU.CBL".
000660			COPY "FDEVIEW.CBL".
000670			COPY "FDEUTEN.CBL".
000680
000690		
000700
000710
000720
000730 WORKING-STORAGE SECTION.
000740
000750
000760          COPY "GLOBALS.CBL".
000770			COPY "IMAGES.CBL".
000780
000790
000800 77 FILENAME			PIC X(50).
000810 77 DATI  			PIC X(1024).
000820
000830 77 SW-TABLE			PIC X.
000840   88 table-on		VALUE "Y".
000850   88 table-oFF		VALUE "N".
000860
000870 77 DEBUG				PIC X.
000880   88 debug-on		VALUE "Y".
000890   88 debug-oFF		VALUE "N".
000900 01 TAB-TABLE.		
000910		02 INDEX-TABLE 	PIC 99.
000920		02 IND-TABLE  	PIC 99.
000930		02 ITEM-TABLE OCCURS 20 TIMES.
000940		 09 NAME-TABLE	PIC X(30).
000950		 09 VALUE-TABLE	PIC X(512).
000960		02 TIMES-TABLE	PIC 99.
000970
000980 77 IND1				PIC 9(9).
000990 77 IND2				PIC 9(9).
001000 77 IND3				PIC 9(9).
001010 
001020  LINKAGE SECTION.
001030
001040 			COPY "COBW3.CBL".
001050
001060  PROCEDURE       DIVISION USING COBW3.
001070  DECLARATIVES.
001080  
001090  MANCA-FILE SECTION.
001100		USE AFTER STANDARD ERROR PROCEDURE ON ARKLIB.
001110  MANCAFILE.
001120   		CONTINUE.
001130  EX-MANCAFILE.
001140	    EXIT.
001150  END DECLARATIVES.
001160
001170
001180		MOVE "N"			TO SW-TABLE.
001190		MOVE "N"			TO DEBUG.
001200
001210		MOVE PAGE-WEB		TO FILE-WEB.
001220
001230		OPEN INPUT ARKWEB.
001240
001250		IF STATUS-WEB NOT = "00" GO TO FINE.
001260
001270 CICLO.
001280		
001290		READ ARKWEB NEXT RECORD AT END GO TO FINE.
001300
001310		IF DATI-WEB = SPACES GO TO CICLO.
001320
001330***** asterisco commenta la riga
001331
001332		PERFORM VARYING IND FROM 1 BY 1 UNTIL DATI-WEB(IND:1) > SPACES
001333       CONTINUE
001334		END-PERFORM
001335
001336		IF DATI-WEB(IND:1) = "*" GO TO CICLO.
001370
001380		MOVE ZERO	TO IND.
001390
001400		INSPECT DATI-WEB TALLYING IND FOR ALL "("
001410		INSPECT DATI-WEB TALLYING IND FOR ALL ")"
001420
001430
001440		IF IND = 1
001450		STRING "mancano (/) " FILE-WEB DELIMITED BY "  "
001460		 " riga: " DATI-WEB 
001470		delimited by size into COBW3-PUT-STRING
001480		PERFORM LINE-WEB THRU EX-LINE-WEB
001490		 GO TO CICLO.
001500
001510
001520		IF IND NOT = 2 GO TO CICLO.
001530		
001540
001550
001560*** softwire switches= debug(value="SI")
001570
001580		MOVE ZERO TO IND.
001590		INSPECT FUNCTION UPPER-CASE(DATI-WEB) TALLYING IND 
001600		 FOR ALL 'DEBUG(ENABLED="SI")'
001610		IF IND = 1
001620		SET DEBUG-ON TO TRUE
001630		go to ciclo.
001640
001650		MOVE ZERO TO IND.
001660		INSPECT FUNCTION UPPER-CASE(DATI-WEB) TALLYING IND 
001670		 FOR ALL 'DEBUG(ENABLED="NO")'
001680		IF IND = 1
001690		SET DEBUG-OFF TO TRUE
001700		go to ciclo.
001710
001720
001730		
001740
001750
001760****  FILENAME nella forma AZIONE(PARAMETRI) DOVE:
001770*
001780*     AZIONE 		(il nome del template da aprire su template\AZIONE.HTM 
001790*     PARAMETRI 	(nella forma NOME="valore")
001800
001810		MOVE SPACES  TO FILENAME DATI INITIALIZE TAB-TABLE.
001820
001830		UNSTRING DATI-WEB DELIMITED BY "("
001840		 INTO FILENAME DATI.
001850
001860		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > LENGTH OF FILENAME
001870		 OR FILENAME(IND:1) > SPACES
001880		 CONTINUE
001890		END-PERFORM
001900	
001910		IF IND NOT > LENGTH OF FILENAME
001920		 MOVE FILENAME(IND:)	TO BUFFER
001930		 MOVE BUFFER			TO FILENAME.
001940
001950
001960**** SOFTWIRED= "HTML"
001970		
001980		IF FUNCTION UPPER-CASE(FILENAME) = "HTML"
001990		 MOVE DATI			TO COBW3-PUT-STRING
002000		 PERFORM VARYING IND FROM LENGTH OF COBW3-PUT-STRING BY -1
002010		 UNTIL IND = ZERO OR COBW3-PUT-STRING(IND:1) = ")"
002020	 	  CONTINUE
002030		 END-PERFORM
002040		 MOVE SPACES		TO COBW3-PUT-STRING(IND:1) 
002050		  PERFORM LINE-WEB THRU EX-LINE-WEB
002060			GO TO CICLO.
002070
002080
002090		 MOVE ZERO TO IND IND1
002100		 INSPECT DATI TALLYING IND FOR ALL "="
002110
002120		 IF IND NOT = ZEROS
002130		 MOVE ZERO TO IND
002140		 INSPECT DATI-WEB TALLYING IND FOR ALL QUOTE
002150		  DIVIDE IND BY  2 GIVING IND REMAINDER IND1
002160		
002170		  IF IND1 = ZEROS	
002180		  PERFORM DO-TABLE	THRU EX-DO-TABLE
002190		   ELSE
002200		   STRING "INDICARE UN NUMERO CORRETTO DI PARAMETRI CON GLI APICI"  DATI-WEB
002210		   DELIMITED BY SIZE INTO  COBW3-PUT-STRING
002220		   PERFORM LINE-WEB THRU EX-LINE-WEB
002230		  END-IF
002240		 END-IF.
002250				
002260
002270
002280
002290				
002300		IF TIMES-TABLE = ZERO MOVE 1 TO TIMES-TABLE.
002310
002320		PERFORM ELABORA-LIBRERIA	THRU EX-ELABORA-LIBRERIA 
002330			TIMES-TABLE  TIMES.
002340
002350		GO TO CICLO.
002360
002370 FINE.
002380
002390		CLOSE ARKWEB.
002400
002410
002420
002430		EXIT PROGRAM.
002440
002450		COPY "PIOABI.CBL".
002460		COPY "PIOMENU.CBL".
002470		
002480			
002490 ELABORA-LIBRERIA.
002500
002510		MOVE SPACES		TO FILE-LIB	
002520		STRING "TEMPLATE\" DELIMITED BY SIZE 
002530			 FUNCTION LOWER-CASE(FILENAME) DELIMITED BY "   " 
002540				".htm" delimited by size into FILE-LIB.
002550
002560
002570		MOVE SPACES		TO COBW3-PUT-STRING
002580
002590		IF DEBUG-ON
002600		STRING 	"<!- " 	delimited by size
002610			 file-lib 	delimited by "   "
002620				"->" 	delimited by SIZE INTO  COBW3-PUT-STRING
002630		PERFORM LINE-WEB THRU EX-LINE-WEB.
002640
002650		OPEN INPUT ARKLIB.
002660	
002670		IF STATUS-LIB = "35"
002680		STRING "template:" FILE-LIB " NON PRESENTE"
002690		 delimited by SIZE INTO  COBW3-PUT-STRING
002700		PERFORM LINE-WEB THRU EX-LINE-WEB
002710		ELSE
002720		PERFORM TRATTA-LIB THRU EX-TRATTA-LIB
002730		CLOSE ARKLIB.
002740
002750 EX-ELABORA-LIBRERIA.
002760		EXIT.
002770
002780 LINE-WEB.
002790*
002800		IF COBW3-PUT-STRING = SPACES GO TO EX-LINE-WEB.
002810
002820		MOVE ZEROS          TO     COBW3-PUT-STRING-LENGTH
002830
002840      CALL  "COBW3_PUT_TEXT"   USING  COBW3
002850
002860
002870
002880		MOVE SPACES             TO COBW3-PUT-STRING.
002890
002900
002910 EX-LINE-WEB.
002920		EXIT.  
002930
002940 TRATTA-LIB.
002950		
002960		READ ARKLIB NEXT RECORD AT END GO TO EX-TRATTA-LIB.
002970
002980		IF DATI-LIB = SPACES GO TO TRATTA-LIB.
002990
003000*** DOPPIO {{  VUOL DIRE CHE NON DEVE ESSERE PROCESSATO COME VARIABILE MA SOLO COME ISTRUZIONE EASYUI E DIVENTA QUINDI {
003010
003020		MOVE ZERO TO IND.
003030		INSPECT DATI-LIB TALLYING IND FOR ALL "{{"
003040		IF IND NOT = ZEROS
003050		INSPECT DATI-LIB REPLACING ALL "{{" BY " {"
003060		INSPECT DATI-LIB REPLACING ALL "}}" BY "} "
003070	    GO TO RICICLO-LIB.
003080
003090
003100
003110		MOVE ZERO TO IND.
003120		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "<TABLE"
003130		IF IND = 1
003140		SET TABLE-ON TO TRUE.
003150
003160		MOVE ZERO TO IND.
003170		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "</TABLE"
003180		IF IND = 1
003190		SET TABLE-OFF TO TRUE.
003200
003210
003220		MOVE ZERO TO IND.
003230		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "</TD"
003240		IF IND = 1 AND TABLE-OFF GO TO TRATTA-LIB.
003250
003260		MOVE ZERO TO IND.
003270		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "<TD"
003280		IF IND = 1 AND TABLE-OFF GO TO TRATTA-LIB.
003290
003300
003310		MOVE ZERO TO IND.
003320		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "</TR"
003330		IF IND = 1 AND TABLE-OFF GO TO TRATTA-LIB.
003340
003350		MOVE ZERO TO IND.
003360		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "<TR"
003370		IF IND = 1 AND TABLE-OFF GO TO TRATTA-LIB.
003380
003390
003400
003410
003420***** asterisco commenta la riga
003430
003440		IF DATI-LIB(1:1) = "*" 
003450
003460		 IF DEBUG-OFF
003470		  GO TO TRATTA-LIB
003480		  ELSE
003490		  STRING "<!- " 	delimited by size
003500		  dati-lib delimited by "   "
003510		    "->" 	delimited by SIZE INTO  COBW3-PUT-STRING
003520	      PERFORM LINE-WEB THRU EX-LINE-WEB
003530		  go to TRATTA-LIB
003540	 	 END-IF
003550	    END-IF.
003560
003570*
003580***  EVENTUALI {STRINGA} PRESENTI IN NAME-TABLE(1.....N) DA USARE PER COSTANTI TITOLO="PROVA"
003590*		 VANNO SOSTITUITI CON VALUE-TABLE(1....N)
003600
003610		PERFORM ESTRAI-STRINGA	THRU EX-ESTRAI-STRINGA.
003620
003630*
003640***  EVENTUALI <COBOL>VARIABILE</COBOL> PRESENTI 			DA USARE PER VARIABILI ALLOCATE SU ARKVIEW.
003650*		 VANNO SOSTITUITI CON RELATIVO VALORE..
003660
003670		PERFORM ESTRAI-COBOL	THRU EX-ESTRAI-COBOL.
003680
003690
003700*
003710***  EVENTUALI @XXXXXYY SOFTWIRED INTERNAL PROCEDURE
003720*
003730
003740		IF FUNCTION UPPER-CASE(DATI-LIB(1:5)) = "@LOAD"
003750       PERFORM OPEN-I-MENU	THRU EX-OPEN-I-MENU
003760		 PERFORM OPEN-I-ABI 	THRU EX-OPEN-I-ABI 
003770		 PERFORM LETTURA-MOD	THRU EX-LETTURA-MOD
003780		 CLOSE ARKMENU ARKABI
003790		  GO TO TRATTA-LIB.
003800
003810 RICICLO-LIB.
003820
003830		MOVE DATI-LIB			TO COBW3-PUT-STRING.
003840
003850		PERFORM LINE-WEB THRU EX-LINE-WEB.
003860
003870		GO TO TRATTA-LIB.
003880
003890 EX-TRATTA-LIB.
003900		EXIT.
003910
003920 ESTRAI-COBOL.
003930
003940		move zeros				TO ind.
003950
003960		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "<COBOL>"
003970
003980		IF IND = ZEROS  GO TO EX-ESTRAI-COBOL.
003990
004000
004010 CICLO-ESTRAI-COBOL.
004020
004030		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > LENGTH OF DATI-LIB
004040		 OR FUNCTION UPPER-CASE(DATI-LIB(IND:7)) = "<COBOL>"
004050		 CONTINUE
004060		END-PERFORM.
004070		
004080		MOVE IND   TO IND-WEB
004090
004100		SUBTRACT 1 FROM IND-WEB.
004110
004120		ADD 7 TO IND
004130
004140		MOVE ZERO TO SIZE-WEB
004150
004160		PERFORM VARYING IND1 FROM IND BY 1 UNTIL IND1 > LENGTH OF DATI-LIB
004170		 OR FUNCTION UPPER-CASE(DATI-LIB(IND1:8)) = "</COBOL>"
004180		 ADD 1 TO SIZE-WEB
004190		END-PERFORM
004200
004210		MOVE DATI-LIB(IND:SIZE-WEB)  TO NOME-VIEW
004220
004230**** ELIMINA </COBOL> dal buffer DATI-LIB
004240
004250		MOVE SPACES TO DATI-LIB(IND1:8)
004260
004270		ADD 8 			TO IND1.
004280
004290
004300  	READ ARKVIEW NO LOCK KEY IS CHIAVE-VIEW
004310    	 INVALID KEY 
004320          MOVE SPACES 			TO EDIT-VIEW.
004330
004340		MOVE EDIT-VIEW				TO BUFFER
004350		PERFORM DIVISORE			THRU EX-DIVISORE
004360
004370		MOVE BUFFER					TO ARGUMENT
004380
004390		MOVE DATI-LIB(1:IND-WEB)	TO BUFFER
004400
004410		PERFORM DIVISORE			THRU EX-DIVISORE
004420
004430		MOVE DATI-LIB(IND1:)		TO BUFFER1		
004440
004450		MOVE SPACES TO DATI-LIB
004460		 STRING BUFFER DELIMITED BY "/DIVISORE/"
004470			 ARGUMENT DELIMITED BY "/DIVISORE/"
004480			  BUFFER1 DELIMITED BY SIZE INTO DATI-LIB.
004490
004500 EX-ESTRAI-COBOL.
004510		EXIT.
004520
004530***** attenzione usare sempre coppie !!! se c'e uno script javascript va in loop...
004540
004550 ESTRAI-STRINGA.
004560
004570		move zeros				TO ind.
004580
004590		INSPECT DATI-LIB TALLYING IND FOR ALL "{"
004600		INSPECT DATI-LIB TALLYING IND FOR ALL "}"
004610
004620		IF IND < 2      GO TO EX-ESTRAI-STRINGA.
004630
004640 CICLO-ESTRAI-STRINGA.
004650
004660		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > LENGTH OF DATI-LIB
004670		 OR DATI-LIB(IND:1) = "{"
004680		 CONTINUE
004690		END-PERFORM.
004700
004710
004720		MOVE IND			TO IND1
004730		MOVE IND1			TO IND2
004740		ADD  1				TO IND2
004750		SUBTRACT 1 FROM IND1
004760
004770		MOVE SPACES			TO FIELD-WEB
004780		MOVE ZEROS 			TO IND3
004790
004800		PERFORM VARYING IND FROM IND2 BY 1 UNTIL IND2 > LENGTH OF DATI-LIB
004810		 OR DATI-LIB(IND:1) = "}"
004820
004830			 ADD 1				  TO IND3
004840			 MOVE DATI-LIB(IND:1) TO FIELD-WEB(IND3:1)
004850			
004860		END-PERFORM.
004870
004880		MOVE DATI-LIB(1:IND1)		TO BUFFER
004890
004900		PERFORM DIVISORE			THRU EX-DIVISORE
004910
004920
004930
004940		ADD 1						TO IND
004950		MOVE DATI-LIB(IND:)			TO BUFFER1		
004960
004970		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND >  20 
004980		 OR FUNCTION UPPER-CASE(FIELD-WEB) = FUNCTION UPPER-CASE(NAME-TABLE(IND))
004990		CONTINUE
005000
005010		END-PERFORM.
005020
005030
005040
005050*** NON C'E
005060
005070		IF IND > 20
005080		 MOVE SPACES TO DATI-LIB
005090		 STRING BUFFER DELIMITED BY "/DIVISORE/"
005100			 " " BUFFER1 DELIMITED BY SIZE INTO DATI-LIB
005110
005120***** ELIMINA LA STRINGA non assegnata che rimane titolo=" "
005130			PERFORM VARYING IND-WEB FROM 1 BY 1 UNTIL IND-WEB > LENGTH OF DATI-LIB
005140
005150			 IF DATI-LIB(IND-WEB:4) = '=" "'
005160			 MOVE SPACES TO DATI-LIB(IND-WEB:4)
005170			 SUBTRACT 1 FROM IND-WEB
005180			 PERFORM VARYING IND-WEB FROM IND-WEB BY -1 UNTIL IND-WEB = ZERO			
005190			 OR DATI-LIB(IND-WEB:1) = SPACES
005200			 MOVE SPACES TO DATI-LIB(IND-WEB:1) 
005210			 END-PERFORM
005220			 END-IF
005230
005240			END-PERFORM
005250			 
005260		ELSE
005261
005270		 MOVE SPACES TO DATI-LIB
005280* item-- combo formato VALORE, STRINGA COMBO ESEMPIO M, MASCHIO la variabile per il confronto con M e' in memoria n.b deve essere 
005290
005300		    IF FUNCTION UPPER-CASE(NAME-TABLE(IND)) = "ITEM"		 
005310		  	PERFORM DO-ITEM		THRU EX-DO-ITEM
005320		  	GO TO EX-ESTRAI-STRINGA
005330	     	END-IF
005340                                                                                                                       
005350		 STRING BUFFER DELIMITED BY "/DIVISORE/"
005360			 VALUE-TABLE(IND) DELIMITED BY "/DIVISORE/"
005370			  BUFFER1 DELIMITED BY SIZE INTO DATI-LIB.
005380
005390		 
005400		GO TO ESTRAI-STRINGA.
005410
005420 EX-ESTRAI-STRINGA.
005430      EXIT.
005440
005450**** VALUE-TABLE(IND) = "M, MASCHIO" > value:'M' , text:'MASCHIO'....
005460
005470 DO-ITEM.
005480
005490		   MOVE SPACES TO ARGUMENT BUFFER BUFFER1 DATI-LIB
005500		   UNSTRING VALUE-TABLE(IND) DELIMITED   ","
005510			 INTO ARGUMENT  BUFFER
005520			 UNSTRING BUFFER DELIMITED BY "/DIVISORE/"
005530			 INTO BUFFER1.
005540*** add a required comma ! be carefull 
005550
005560			INSPECT BUFFER1 REPLACING ALL "' iconCls:" 
005570								 BY  	  "',iconCls:" 
005580
005590** clear the item (so I'll process next in the table list
005600
005610			 INITIALIZE ITEM-TABLE (IND).
005620
005630**** 
005640			 IF ARGUMENT = EDIT-VIEW 
005650			 MOVE SPACES TO DATI-LIB
005660			 STRING "          { value:'" DELIMITED BY SIZE 
005670								ARGUMENT  DELIMITED BY "    "
005680							   "',selected:true, text:'" 
005690										  DELIMITED BY SIZE 
005700								BUFFER1   DELIMITED BY "    "
005710								"'},"	  DELIMITED BY SIZE 
005720			 INTO DATI-LIB
005730			ELSE
005740			 STRING "          { value:'" DELIMITED BY SIZE 
005750								ARGUMENT  DELIMITED BY "    "
005760							   "',text:'" DELIMITED BY SIZE 
005770								BUFFER1   DELIMITED BY "    "
005780								"'},"	  DELIMITED BY SIZE 
005790			 INTO DATI-LIB.
005800
005810			IF IND < INDEX-TABLE
005820			 AND FUNCTION UPPER-CASE(NAME-TABLE(IND + 1)) NOT = "ITEM"
005830			 INSPECT DATI-LIB REPLACING ALL "}," BY "} ".
005840
005850 EX-DO-ITEM.
005860		EXIT.
005870
005880 DIVISORE.
005890
005900	    PERFORM VARYING IND4-WEB FROM LENGTH OF BUFFER   
005910			BY -1 UNTIL BUFFER(IND4-WEB:1) > SPACES
005920				OR IND4-WEB = ZEROS
005930				CONTINUE
005940		  END-PERFORM.
005950
005960		ADD 1 TO IND4-WEB.
005970
005980		MOVE "/DIVISORE/" TO BUFFER(IND4-WEB:).
005990
006000 EX-DIVISORE.
006010		EXIT.
006020
006030
006040 DO-TABLE.
006050
006060		MOVE 0 					TO INDEX-TABLE.
006070		MOVE 1					TO IND.
006080
006090 CICLO-TABLE.
006100
006110		 ADD  1 				TO INDEX-TABLE.
006120
006130
006140*** SI POSIZIONA SUL PRIMO CARATTERE  SIGNIFICATIVO
006150
006160		PERFORM VARYING IND FROM IND BY 1 UNTIL IND > LENGTH OF DATI
006170		 OR DATI(IND:1) > SPACES
006180		 CONTINUE
006190		END-PERFORM.
006200
006210		IF IND > LENGTH OF DATI 
006220		GO TO END-DO-TABLE.
006230
006240		MOVE 1					TO IND-TABLE
006250
006260		PERFORM VARYING IND FROM IND BY 1 UNTIL IND > LENGTH OF DATI
006270		 OR DATI(IND:1) = "="
006280
006290			MOVE DATI(IND:1)	TO NAME-TABLE( INDEX-TABLE) (IND-TABLE:1) 
006300			ADD 1				TO IND-TABLE
006310
006320		 CONTINUE
006330		END-PERFORM.
006340
006350		IF IND > LENGTH OF DATI 
006360		GO TO END-DO-TABLE.
006370
006380
006390		IF DATI(IND:1) = "="
006400		ADD 1		TO IND
006410		MOVE 1		TO IND-TABLE
006420
006430****  SI POSIZIONA SUL PRIMO '"' DOPO '=" esempio title="TESTO"  
006440*                                                       ! < qui
006450
006460		PERFORM VARYING IND FROM IND BY 1 UNTIL IND > LENGTH OF DATI
006470		 OR DATI(IND:1) = '"'
006480		 CONTINUE
006490		END-PERFORM
006500
006510		ADD 1				TO IND
006520
006530		PERFORM VARYING IND FROM IND BY 1 UNTIL IND > LENGTH OF DATI
006540		 OR DATI(IND:1) = '"'
006550
006560			MOVE DATI(IND:1)	TO VALUE-TABLE( INDEX-TABLE) (IND-TABLE:1) 
006570			ADD 1				TO IND-TABLE
006580
006590		 CONTINUE
006600		END-PERFORM
006610
006620****SOFWIRED update: size--> width:
006630
006640		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "SIZE"
006650		MOVE VALUE-TABLE(INDEX-TABLE)		TO ARGUMENT
006660		 MOVE SPACES TO VALUE-TABLE(INDEX-TABLE)
006670		STRING "width:" DELIMITED BY SIZE ARGUMENT DELIMITED BY "    " 
006680			";"	 DELIMITED BY SIZE INTO VALUE-TABLE(INDEX-TABLE)
006690		END-IF
006700
006710
006720		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "TYPE"
006730		MOVE VALUE-TABLE(INDEX-TABLE)		TO ARGUMENT
006740		 IF FUNCTION UPPER-CASE(ARGUMENT) = "MAIL"
006750		 MOVE "validType:'email' ," TO VALUE-TABLE(INDEX-TABLE)
006760		END-IF
006770		END-IF.
006780
006790
006800
006810
006820		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "TIMES"
006830			 MOVE VALUE-TABLE(INDEX-TABLE)		TO TIMES-TABLE(1:)
006840			 IF TIMES-TABLE(2:1) = SPACES
006850			 MOVE TIMES-TABLE(1:1)	TO TIMES-TABLE(2:1)
006860			 MOVE 0					TO TIMES-TABLE(1:1).
006870
006880***** CE' UN ELEMENTO DI MENU: DEVE CONTROLLARE SE ABILITATO O MENO
006890		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "DISABLED"
006900		 PERFORM OPEN-I-ABI 			THRU EX-OPEN-I-ABI 
006910
006920		 INITIALIZE ABILITAZIONE
006930
006940		 MOVE VALUE-TABLE(INDEX-TABLE)	TO MOD-ABI
006950		 MOVE 01					    TO ENTE-ABI
006960  	 IF GRUPPO-UTEN = ZEROS MOVE 1  TO GRUPPO-UTEN END-IF
006970		 MOVE GRUPPO-UTEN			    TO GRUPPO-ABI
006980		  
006990		 PERFORM LEGGO-ABI				THRU EX-LEGGO-ABI
007000
007010		 IF ESITO-NOK MOVE "true "		TO VALUE-TABLE(INDEX-TABLE)
007020
007030			IF NOME-UTEN = "ADMIN" 
007040		 		OR GRUPPO-UTEN = 99
007050				MOVE "false"			TO VALUE-TABLE(INDEX-TABLE)
007060			end-if
007070
007080		ELSE
007090		 MOVE "false"					TO VALUE-TABLE(INDEX-TABLE)
007100		 end-if
007110
007120		 CLOSE ARKABI.
007130
007140
007150
007160		PERFORM VARYING IND-WEB FROM LENGTH OF VALUE-TABLE(INDEX-TABLE) BY -1
007170		 UNTIL VALUE-TABLE(INDEX-TABLE) (IND-WEB:1) > SPACES
007180			OR IND-WEB = ZERO
007190		 CONTINUE
007200		END-PERFORM
007210
007220		ADD 1 TO IND-WEB
007230		MOVE "/DIVISORE/" TO VALUE-TABLE(INDEX-TABLE) (IND-WEB:).
007240		
007250
007260
007270		IF IND NOT > LENGTH OF DATI
007280		 ADD 1 					TO IND
007290		 GO TO CICLO-TABLE.
007300
007310 END-DO-TABLE.
007320
007330		SUBTRACT 1 FROM INDEX-TABLE
007340
007350		IF DEBUG-ON
007360
007370		MOVE 1					TO IND
007380
007390		PERFORM INDEX-TABLE TIMES
007400
007410		STRING 	"<!- " 	delimited by size
007420			 " Ind "  IND  " "
007430				NAME-TABLE(IND) DELIMITED BY "    "			
007440					 " contiene: " DELIMITED BY SIZE
007450				VALUE-TABLE(IND) DELIMITED BY "    "			
007460				" ->" 	delimited by SIZE INTO  COBW3-PUT-STRING
007470		PERFORM LINE-WEB THRU EX-LINE-WEB
007480
007490		ADD 1 TO IND
007500
007510		END-PERFORM.
007520
007530
007540 EX-DO-TABLE.
007550		EXIT. 
007560
007570**** DUMMY
007580 VIS-MESS. 
007590 EX-VIS-MESS.
007600		EXIT.
007610
007620
007630
007640**** DEVE ESISTERE UNA VARIABILE {menu} IN TABELLA
007650
007660 LETTURA-MOD.
007670
007680          INITIALIZE MENU.
007690
007700			PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > INDEX-TABLE
007710
007720			IF FUNCTION UPPER-CASE(NAME-TABLE(IND)) = "MENU"			
007730			  MOVE VALUE-TABLE(IND)		TO DATI-LIB(6:2)
007740			END-IF
007750				
007760			END-PERFORM.
007770
007780          MOVE DATI-LIB(6:2)      TO DEP-MOD.
007790
007800          PERFORM STARTO-MENU     THRU EX-STARTO-MENU.
007810          IF ESITO-NOK
007820          STRING  "MANCA MENU" MOD-MENU DELIMITED BY SIZE INTO MESSAGGIO
007830           PERFORM VIS-MESS    THRU EX-VIS-MESS
007840            GO TO EX-LETTURA-MOD.
007850
007860          SET FIRST-TIME  TO TRUE.
007870
007880 CICLO-LETTURA-MOD.
007890
007900          PERFORM LEGGO-NEXT-MENU THRU EX-LEGGO-NEXT-MENU.
007910
007920          IF FINE-FILE = "S" GO TO END-LETTURA-MOD.
007930
007940          IF MOD-MENU NOT = DEP-MOD GO CICLO-LETTURA-MOD.
007950
007960          IF ENT-MENU = SPACES GO TO CICLO-LETTURA-MOD.
007970
007980
007990          IF FUNCTION UPPER-CASE(DESC-MENU) = "SEPARATORE"
008000           MOVE '        <div class="menu-sep"></div>' TO DATI-WEB
008010           PERFORM PREPARA-LINE-WEB        THRU EX-PREPARA-LINE-WEB
008020            GO TO CICLO-LETTURA-MOD.
008030
008040          INITIALIZE ABILITAZIONE.
008050
008060          MOVE GRUPPO-UTEN        TO GRUPPO-ABI.
008070          IF GRUPPO-ABI = ZEROS
008080            MOVE 1                TO GRUPPO-ABI.
008090
008100          MOVE 01                 TO ENTE-ABI.
008110          MOVE CHIAVE-SEC-MENU    TO CHIAMATA-ABI.
008120
008130          PERFORM ABILITAZIONI    THRU EX-ABILITAZIONI.
008140
008150
008160          IF FUNZ-MENU = SPACES
008170              PERFORM MAKE-ENTITA   THRU EX-MAKE-ENTITA
008180           ELSE
008190              PERFORM MAKE-FUNZIONE THRU EX-MAKE-FUNZIONE.
008200
008210          GO TO CICLO-LETTURA-MOD.
008220
008230 END-LETTURA-MOD.
008240
008250          MOVE "          </div></div>"     TO DATI-WEB
008260          PERFORM PREPARA-LINE-WEB                  THRU EX-PREPARA-LINE-WEB.
008270
008280 EX-LETTURA-MOD.
008290          EXIT.
008300
008310 ABILITAZIONI.
008320
008330          PERFORM LEGGO-ABI       THRU EX-LEGGO-ABI.
008340
008350          IF ESITO-NOK
008360           SET DISABLED           TO TRUE.
008370
008380          IF NOME-UTEN = "ADMIN" OR GRUPPO-UTEN > 99
008390           MOVE "N"                TO FLAG-DISABLED.
008400
008410 EX-ABILITAZIONI.
008420          EXIT.
008430 MAKE-ENTITA.
008440
008450          IF NOT FIRST-TIME
008460          MOVE   "        </div></div>"     TO DATI-WEB
008470          PERFORM PREPARA-LINE-WEB        THRU EX-PREPARA-LINE-WEB
008480          ELSE
008490          MOVE "N"                        TO FLAG-TIME.
008500
008510
008520
008530          IF DISABLED
008540                  MOVE "disabled:true"            to wdisable
008550          else
008560                  MOVE "disabled:false"           to wdisable.
008570
008580          IF IMG-MENU > ZEROS
008590                  AND IMG-MENU < 20
008600                  MOVE WIMAGE(IMG-MENU)           TO WIMAGES
008610
008620              else
008630                  MOVE SPACES                     to wIMAGES.
008640
008650			MOVE SPACES TO DATI-WEB.
008660
008670
008680          STRING "             " DELIMITED BY SIZE
008690          '<div data-options="'
008700                  wdisable delimited by  "  "
008710                  wimages delimited by "   "
008720                   '"><span>' delimited BY  SIZE
008730          DESC-MENU DELIMITED BY "    " "</span><div>"
008740              DELIMITED BY SIZE INTO DATI-WEB.
008750
008760          PERFORM PREPARA-LINE-WEB        THRU EX-PREPARA-LINE-WEB.
008770
008780
008790 EX-MAKE-ENTITA.
008800          EXIT.
008810
008820
008830
008840 MAKE-FUNZIONE.
008850
008860
008870          IF DISABLED
008880          string
008890          '            <div data-options="disabled:true">'
008900          DESC-MENU DELIMITED BY "     " "</div>"
008910              DELIMITED BY SIZE INTO DATI-WEB
008920          ELSE
008930          STRING
008940          '            <div>'  DELIMITED BY SIZE
008950          DESC-MENU DELIMITED BY "     " "</div>"
008960              DELIMITED BY SIZE INTO DATI-WEB.
008970
008980          IF DISABLED
008990                  MOVE "disabled:true"            to wdisable
009000                  else
009010                  MOVE "disabled:false"           to wdisable.
009020
009030          IF IMG-MENU > ZEROS
009040                  AND IMG-MENU < 20
009050                  MOVE WIMAGE(IMG-MENU)           TO WIMAGES
009060
009070                  else
009080                  MOVE SPACES                     to wIMAGES.
009090
009100			MOVE SPACES TO DATI-WEB.
009110
009120          STRING "             " DELIMITED BY SIZE
009130                  '<div data-options="'
009140                  wdisable delimited by  "  "
009150                  wimages delimited by "   "
009160                  '"><a style="text-decoration:none;" href="' DELIMITED BY SIZE
009170                  PROG-MENU DELIMITED BY "   "
009180                  ".exe?MK-KEY=" SECTION-WEB DELIMITED BY SIZE
009190					"&MK-ENTITA=" ENT-MENU DELIMITED BY SIZE 
009200					"&MK-FUNZIONE=" FUNZ-MENU DELIMITED BY SIZE
009210                  '" class="easyui-menulink">' DELIMITED BY SIZE 
009220		            DESC-MENU DELIMITED BY "    " 
009230				    "</a></div>"
009240                  DELIMITED BY SIZE INTO DATI-WEB.
009250
009260          PERFORM PREPARA-LINE-WEB        THRU EX-PREPARA-LINE-WEB.
009270
009280
009290 EX-MAKE-FUNZIONE.
009300          EXIT.
009310
009320 PREPARA-LINE-WEB.
009330
009340			MOVE DATI-WEB					TO COBW3-PUT-STRING.
009350
009360		    PERFORM LINE-WEB 				THRU EX-LINE-WEB.
009370
009380 EX-PREPARA-LINE-WEB.
009390			EXIT.
