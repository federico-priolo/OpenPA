000010 @OPTIONS ALPHAL(WORD)
000020*
000030* Copyright (C) 2022 Federico Priolo TP ONE SRL federico.priolo@tp-one.it
000040*
000050* This program is free software; you can redistribute it and/or modify
000060* it under the terms of the GNU General Public License as published by
000070* the Free Software Foundation; either version 2, or (at your option)
000080* any later version.
000090*
000100* This program is distributed in the hope that it will be useful,
000110* but WITHOUT ANY WARRANTY; without even the implied warranty of
000120* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
000130* GNU General Public License for more details.
000140*
000150* You should have received a copy of the GNU General Public License
000160* along with this software; see the file COPYING.  If not, write to
000170* the Free Software Foundation, 51 Franklin Street, Fifth Floor
000180* Boston, MA 02110-1301 USA
000190*
000200
000210*
000220 identification division.
000230 PROGRAM-ID. MAKEFORM.
000240 AUTHOR. FEDERICO PRIOLO AGOSTO 2022
000250
000260*
000270* Copyright (C) 2010-2022 Federico Priolo TP ONE SRL federico.priolo@tp-one.it
000280*
000290* This program is free software; you can redistribute it and/or modify
000300* it under the terms of the GNU General Public License as published by
000310* the Free Software Foundation; either version 2, or (at your option)
000320* any later version.
000330*
000340* This program is distributed in the hope that it will be useful,
000350* but WITHOUT ANY WARRANTY; without even the implied warranty of
000360* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
000370* GNU General Public License for more details.
000380*
000390* You should have received a copy of the GNU General Public License
000400* along with this software; see the file COPYING.  If not, write to
000410* the Free Software Foundation, 51 Franklin Street, Fifth Floor
000420* Boston, MA 02110-1301 USA
000430*
000440 ENVIRONMENT DIVISION.
000450 CONFIGURATION    SECTION.
000460
000470          COPY "SPECIAL.CBL".
000480
000490 INPUT-OUTPUT SECTION.
000500 FILE-CONTROL.
000510
000520*
000530			COPY "SELWEB.CBL".
000540			COPY "SELMENU.CBL".
000550			COPY "SELABI.CBL".
000560			COPY "SELVIEW.CBL".
000570			COPY "SELUTEN.CBL".
000580
000590 DATA DIVISION.
000600 FILE SECTION.
000610
000620
000630			COPY "FDEWEB.CBL".
000640			COPY "FDEABI.CBL".
000650			COPY "FDEMENU.CBL".
000660			COPY "FDEVIEW.CBL".
000670			COPY "FDEUTEN.CBL".
000680
000690		
000700
000710
000720
000730 WORKING-STORAGE SECTION.
000740
000750
000760          COPY "GLOBALS.CBL".
000770			COPY "IMAGES.CBL".
000780
000790
000800 77 FILENAME			PIC X(50).
000810 77 DATI  			PIC X(1024).
000811
000812 77 SW-TABLE			PIC X.
000813   88 table-on		VALUE "Y".
000814   88 table-oFF		VALUE "N".
000815
000820 77 DEBUG				PIC X.
000830   88 debug-on		VALUE "Y".
000840   88 debug-oFF		VALUE "N".
000850 01 TAB-TABLE.		
000860		02 INDEX-TABLE 	PIC 99.
000870		02 IND-TABLE  	PIC 99.
000880		02 ITEM-TABLE OCCURS 20 TIMES.
000890		 09 NAME-TABLE	PIC X(30).
000900		 09 VALUE-TABLE	PIC X(512).
000910		02 TIMES-TABLE	PIC 99.
000920
000930 77 IND1				PIC 9(9).
000940 77 IND2				PIC 9(9).
000950 77 IND3				PIC 9(9).
000960 
000970  LINKAGE SECTION.
000980
000990 			COPY "COBW3.CBL".
001000
001010  PROCEDURE       DIVISION USING COBW3.
001020  DECLARATIVES.
001030  MANCA-FILE SECTION.
001040		USE AFTER STANDARD ERROR PROCEDURE ON ARKLIB.
001050  MANCAFILE.
001060   		CONTINUE.
001070  EX-MANCAFILE.
001080	    EXIT.
001090  END DECLARATIVES.
001100
001110
001120		MOVE "N"			TO SW-TABLE.
001121		MOVE "N"			TO DEBUG.
001130
001140		MOVE PAGE-WEB		TO FILE-WEB.
001150
001160		OPEN INPUT ARKWEB.
001170
001180		IF STATUS-WEB NOT = "00" GO TO FINE.
001190
001200 CICLO.
001210		
001220		READ ARKWEB NEXT RECORD AT END GO TO FINE.
001230
001240		IF DATI-WEB = SPACES GO TO CICLO.
001250
001260***** asterisco commenta la riga
001270
001280		IF DATI-WEB(1:1) = "*" GO TO CICLO.
001290
001300
001310		MOVE ZERO	TO IND.
001320
001330		INSPECT DATI-WEB TALLYING IND FOR ALL "("
001340		INSPECT DATI-WEB TALLYING IND FOR ALL ")"
001341
001342
001343		IF IND = 1
001344		STRING "manca PARENTESI su " DATI-WEB 
001345		delimited by size into COBW3-PUT-STRING
001346		PERFORM LINE-WEB THRU EX-LINE-WEB
001347		 GO TO CICLO.
001348
001353
001360		IF IND NOT = 2 GO TO CICLO.
001370		
001470
001480
001590*** softwire switches= debug(value="SI")
001591
001592		MOVE ZERO TO IND.
001593		INSPECT FUNCTION UPPER-CASE(DATI-WEB) TALLYING IND 
001594		 FOR ALL 'DEBUG(ENABLED="SI")'
001595		IF IND = 1
001596		SET DEBUG-ON TO TRUE
001597		go to ciclo.
001598
001599		MOVE ZERO TO IND.
001600		INSPECT FUNCTION UPPER-CASE(DATI-WEB) TALLYING IND 
001601		 FOR ALL 'DEBUG(ENABLED="NO")'
001602		IF IND = 1
001603		SET DEBUG-OFF TO TRUE
001604		go to ciclo.
001605
001606
001607		
001610
001620
001630****  FILENAME nella forma AZIONE(PARAMETRI) DOVE:
001640*
001650*     AZIONE 		(il nome del template da aprire su template\AZIONE.HTM 
001660*     PARAMETRI 	(nella forma NOME="valore")
001670
001680		MOVE SPACES  TO FILENAME DATI INITIALIZE TAB-TABLE.
001690
001700		UNSTRING DATI-WEB DELIMITED BY "("
001710		 INTO FILENAME DATI.
001711
001712		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > LENGTH OF FILENAME
001713		 OR FILENAME(IND:1) > SPACES
001714		 CONTINUE
001715		END-PERFORM
001716	
001717		IF IND NOT > LENGTH OF FILENAME
001718		 MOVE FILENAME(IND:)	TO BUFFER
001719		 MOVE BUFFER			TO FILENAME.
001720
001721
001730**** SOFTWIRED= "HTML"
001740		
001750		IF FUNCTION UPPER-CASE(FILENAME) = "HTML"
001760		 MOVE DATI			TO COBW3-PUT-STRING
001770		 PERFORM VARYING IND FROM LENGTH OF COBW3-PUT-STRING BY -1
001780		 UNTIL IND = ZERO OR COBW3-PUT-STRING(IND:1) = ")"
001790	 	  CONTINUE
001800		 END-PERFORM
001810		 MOVE SPACES		TO COBW3-PUT-STRING(IND:1) 
001820		  PERFORM LINE-WEB THRU EX-LINE-WEB
001830			GO TO CICLO.
001840
001850
001860		 MOVE ZERO TO IND IND1
001870		 INSPECT DATI TALLYING IND FOR ALL "="
001893
001894		 IF IND NOT = ZEROS
001895		 MOVE ZERO TO IND
001896		 INSPECT DATI-WEB TALLYING IND FOR ALL QUOTE
001897		  DIVIDE IND BY  2 GIVING IND REMAINDER IND1
001898		
001899		  IF IND1 = ZEROS	
001900		  PERFORM DO-TABLE	THRU EX-DO-TABLE
001901		   ELSE
001902		   STRING "INDICARE UN NUMERO CORRETTO DI PARAMETRI CON GLI APICI"  DATI-WEB
001903		   DELIMITED BY SIZE INTO  COBW3-PUT-STRING
001904		   PERFORM LINE-WEB THRU EX-LINE-WEB
001905		  END-IF
001906		 END-IF.
001907				
001908
001909
001910
001911				
001912		IF TIMES-TABLE = ZERO MOVE 1 TO TIMES-TABLE.
001920
001930		PERFORM ELABORA-LIBRERIA	THRU EX-ELABORA-LIBRERIA 
001940			TIMES-TABLE  TIMES.
001950
001960		GO TO CICLO.
001970
001980 FINE.
001990
002000		CLOSE ARKWEB.
002010
002020
002030
002040		EXIT PROGRAM.
002050
002060		COPY "PIOABI.CBL".
002070		COPY "PIOMENU.CBL".
002080		
002090			
002100 ELABORA-LIBRERIA.
002110
002120		MOVE SPACES		TO FILE-LIB	
002130		STRING "TEMPLATE\" DELIMITED BY SIZE 
002140			 FUNCTION LOWER-CASE(FILENAME) DELIMITED BY "   " 
002150				".htm" delimited by size into FILE-LIB.
002160
002170
002180		MOVE SPACES		TO COBW3-PUT-STRING
002190
002200		IF DEBUG-ON
002210		STRING 	"<!- " 	delimited by size
002220			 file-lib 	delimited by "   "
002230				"->" 	delimited by SIZE INTO  COBW3-PUT-STRING
002240		PERFORM LINE-WEB THRU EX-LINE-WEB.
002250
002260		OPEN INPUT ARKLIB.
002270	
002280		IF STATUS-LIB = "35"
002290		STRING "template:" FILE-LIB " NON PRESENTE"
002300		 delimited by SIZE INTO  COBW3-PUT-STRING
002310		PERFORM LINE-WEB THRU EX-LINE-WEB
002320		ELSE
002330		PERFORM TRATTA-LIB THRU EX-TRATTA-LIB
002340		CLOSE ARKLIB.
002350
002360 EX-ELABORA-LIBRERIA.
002370		EXIT.
002380
002390 LINE-WEB.
002400*
002410		IF COBW3-PUT-STRING = SPACES GO TO EX-LINE-WEB.
002420
002430		MOVE ZEROS          TO     COBW3-PUT-STRING-LENGTH
002440
002450      CALL  "COBW3_PUT_TEXT"   USING  COBW3
002460
002470
002480
002490		MOVE SPACES             TO COBW3-PUT-STRING.
002500
002510
002520 EX-LINE-WEB.
002530		EXIT.  
002540
002550 TRATTA-LIB.
002560		
002570		READ ARKLIB NEXT RECORD AT END GO TO EX-TRATTA-LIB.
002580
002590		IF DATI-LIB = SPACES GO TO TRATTA-LIB.
002591
002592*** DOPPIO {{  VUOL DIRE CHE NON DEVE ESSERE PROCESSATO COME VARIABILE MA SOLO COME ISTRUZIONE EASYUI E DIVENTA QUINDI {
002593
002601		MOVE ZERO TO IND.
002602		INSPECT DATI-LIB TALLYING IND FOR ALL "{{"
002603		IF IND NOT = ZEROS
002604		INSPECT DATI-LIB REPLACING ALL "{{" BY " {"
002605		INSPECT DATI-LIB REPLACING ALL "}}" BY "} "
002606	    GO TO RICICLO-LIB.
002607
002611
002621
002648		MOVE ZERO TO IND.
002649		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "<TABLE"
002650		IF IND = 1
002651		SET TABLE-ON TO TRUE.
002652
002653		MOVE ZERO TO IND.
002654		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "</TABLE"
002655		IF IND = 1
002656		SET TABLE-OFF TO TRUE.
002657
002658
002659		MOVE ZERO TO IND.
002660		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "</TD"
002661		IF IND = 1 AND TABLE-OFF GO TO TRATTA-LIB.
002662
002663		MOVE ZERO TO IND.
002664		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "<TD"
002665		IF IND = 1 AND TABLE-OFF GO TO TRATTA-LIB.
002666
002667
002668		MOVE ZERO TO IND.
002669		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "</TR"
002670		IF IND = 1 AND TABLE-OFF GO TO TRATTA-LIB.
002671
002672		MOVE ZERO TO IND.
002673		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "<TR"
002674		IF IND = 1 AND TABLE-OFF GO TO TRATTA-LIB.
002675
002676
002677
002678
002679***** asterisco commenta la riga
002680
002681		IF DATI-LIB(1:1) = "*" 
002682
002683		 IF DEBUG-OFF
002684		  GO TO TRATTA-LIB
002685		  ELSE
002686		  STRING "<!- " 	delimited by size
002690		  dati-lib delimited by "   "
002700		    "->" 	delimited by SIZE INTO  COBW3-PUT-STRING
002710	      PERFORM LINE-WEB THRU EX-LINE-WEB
002720		  go to TRATTA-LIB
002730	 	 END-IF
002740	    END-IF.
002750
002760*
002770***  EVENTUALI {STRINGA} PRESENTI IN NAME-TABLE(1.....N) DA USARE PER COSTANTI TITOLO="PROVA"
002780*		 VANNO SOSTITUITI CON VALUE-TABLE(1....N)
002790
002800		PERFORM ESTRAI-STRINGA	THRU EX-ESTRAI-STRINGA.
002810
002820*
002830***  EVENTUALI <COBOL>VARIABILE</COBOL> PRESENTI 			DA USARE PER VARIABILI ALLOCATE SU ARKVIEW.
002840*		 VANNO SOSTITUITI CON RELATIVO VALORE..
002850
002860		PERFORM ESTRAI-COBOL	THRU EX-ESTRAI-COBOL.
002870
002880
002890*
002900***  EVENTUALI @XXXXXYY SOFTWIRED INTERNAL PROCEDURE
002910*
002920
002930		IF FUNCTION UPPER-CASE(DATI-LIB(1:5)) = "@LOAD"
002940       PERFORM OPEN-I-MENU	THRU EX-OPEN-I-MENU
002950		 PERFORM OPEN-I-ABI 	THRU EX-OPEN-I-ABI 
002960		 PERFORM LETTURA-MOD	THRU EX-LETTURA-MOD
002970		 CLOSE ARKMENU ARKABI
002980		  GO TO TRATTA-LIB.
002981
002990 RICICLO-LIB.
002991
003000		MOVE DATI-LIB			TO COBW3-PUT-STRING.
003010
003020		PERFORM LINE-WEB THRU EX-LINE-WEB.
003030
003040		GO TO TRATTA-LIB.
003050
003060 EX-TRATTA-LIB.
003070		EXIT.
003080
003090 ESTRAI-COBOL.
003100
003110		move zeros				TO ind.
003120
003130		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "<COBOL>"
003140
003150		IF IND = ZEROS  GO TO EX-ESTRAI-COBOL.
003160
003170
003180 CICLO-ESTRAI-COBOL.
003190
003200		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > LENGTH OF DATI-LIB
003210		 OR FUNCTION UPPER-CASE(DATI-LIB(IND:7)) = "<COBOL>"
003220		 CONTINUE
003230		END-PERFORM.
003240		
003250		MOVE IND   TO IND-WEB
003260
003270		SUBTRACT 1 FROM IND-WEB.
003280
003290		ADD 7 TO IND
003300
003310		MOVE ZERO TO SIZE-WEB
003320
003330		PERFORM VARYING IND1 FROM IND BY 1 UNTIL IND1 > LENGTH OF DATI-LIB
003340		 OR FUNCTION UPPER-CASE(DATI-LIB(IND1:8)) = "</COBOL>"
003350		 ADD 1 TO SIZE-WEB
003360		END-PERFORM
003370
003380		MOVE DATI-LIB(IND:SIZE-WEB)  TO NOME-VIEW
003390
003400**** ELIMINA </COBOL> dal buffer DATI-LIB
003410
003420		MOVE SPACES TO DATI-LIB(IND1:8)
003430
003440		ADD 8 			TO IND1.
003450
003460
003470  	READ ARKVIEW NO LOCK KEY IS CHIAVE-VIEW
003480    	 INVALID KEY 
003490          MOVE SPACES 			TO EDIT-VIEW.
003520
003530		MOVE EDIT-VIEW				TO BUFFER
003540		PERFORM DIVISORE			THRU EX-DIVISORE
003550
003560		MOVE BUFFER					TO ARGUMENT
003570
003580		MOVE DATI-LIB(1:IND-WEB)	TO BUFFER
003590
003600		PERFORM DIVISORE			THRU EX-DIVISORE
003610
003620		MOVE DATI-LIB(IND1:)		TO BUFFER1		
003630
003640		MOVE SPACES TO DATI-LIB
003650		 STRING BUFFER DELIMITED BY "/DIVISORE/"
003660			 ARGUMENT DELIMITED BY "/DIVISORE/"
003670			  BUFFER1 DELIMITED BY SIZE INTO DATI-LIB.
003680
003690 EX-ESTRAI-COBOL.
003700		EXIT.
003710
003720***** attenzione usare sempre coppie !!! se c'e uno script javascript va in loop...
003730
003740 ESTRAI-STRINGA.
003750
003760		move zeros				TO ind.
003770
003780		INSPECT DATI-LIB TALLYING IND FOR ALL "{"
003790		INSPECT DATI-LIB TALLYING IND FOR ALL "}"
003800
003810		IF IND < 2      GO TO EX-ESTRAI-STRINGA.
003820
003830 CICLO-ESTRAI-STRINGA.
003840
003850		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > LENGTH OF DATI-LIB
003860		 OR DATI-LIB(IND:1) = "{"
003870		 CONTINUE
003880		END-PERFORM.
003890
003900
003910		MOVE IND			TO IND1
003920		MOVE IND1			TO IND2
003930		ADD  1				TO IND2
003940		SUBTRACT 1 FROM IND1
003950
003960		MOVE SPACES			TO FIELD-WEB
003970		MOVE ZEROS 			TO IND3
003980
003990		PERFORM VARYING IND FROM IND2 BY 1 UNTIL IND2 > LENGTH OF DATI-LIB
004000		 OR DATI-LIB(IND:1) = "}"
004010
004020			 ADD 1				  TO IND3
004030			 MOVE DATI-LIB(IND:1) TO FIELD-WEB(IND3:1)
004040			
004050		END-PERFORM.
004060
004070		MOVE DATI-LIB(1:IND1)		TO BUFFER
004080
004090		PERFORM DIVISORE			THRU EX-DIVISORE
004100
004110
004120
004130		ADD 1						TO IND
004140		MOVE DATI-LIB(IND:)			TO BUFFER1		
004150
004160		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND >  20 
004170		 OR FUNCTION UPPER-CASE(FIELD-WEB) = FUNCTION UPPER-CASE(NAME-TABLE(IND))
004180		CONTINUE
004190
004200		END-PERFORM.
004210
004220
004230
004240*** NON C'E
004250
004260		IF IND > 20
004270		 MOVE SPACES TO DATI-LIB
004280		 STRING BUFFER DELIMITED BY "/DIVISORE/"
004290			 " " BUFFER1 DELIMITED BY SIZE INTO DATI-LIB
004300
004310***** ELIMINA LA STRINGA non assegnata che rimane titolo=" "
004320			PERFORM VARYING IND-WEB FROM 1 BY 1 UNTIL IND-WEB > LENGTH OF DATI-LIB
004330
004340			 IF DATI-LIB(IND-WEB:4) = '=" "'
004350			 MOVE SPACES TO DATI-LIB(IND-WEB:4)
004360			 SUBTRACT 1 FROM IND-WEB
004370			 PERFORM VARYING IND-WEB FROM IND-WEB BY -1 UNTIL IND-WEB = ZERO			
004380			 OR DATI-LIB(IND-WEB:1) = SPACES
004390			 MOVE SPACES TO DATI-LIB(IND-WEB:1) 
004400			 END-PERFORM
004410			 END-IF
004420
004430			END-PERFORM
004440			 
004450		ELSE
004460		 MOVE SPACES TO DATI-LIB
004470		 STRING BUFFER DELIMITED BY "/DIVISORE/"
004480			 VALUE-TABLE(IND) DELIMITED BY "/DIVISORE/"
004490			  BUFFER1 DELIMITED BY SIZE INTO DATI-LIB.
004500
004510		 
004520		GO TO ESTRAI-STRINGA.
004530				
004540
004550		
004560
004570 EX-ESTRAI-STRINGA.
004580		EXIT.
004581
004582**** VALUE-TABLE(IND) = "M, MASCHIO" > value:'M' , text:'MASCHIO'....
004583
004584 DO-ITEM.
004585
004586		   MOVE SPACES TO ARGUMENT BUFFER BUFFER1 DATI-LIB
004587		   UNSTRING VALUE-TABLE(IND) DELIMITED   ","
004588			 INTO ARGUMENT  BUFFER
004589			 UNSTRING BUFFER DELIMITED BY "/DIVISORE/"
004590			 INTO BUFFER1.
004591*** add a required comma ! be carefull 
004592
004593			INSPECT BUFFER1 REPLACING ALL "' iconCls:" 
004594								 BY  	  "',iconCls:" 
004595
004596** clear the item (so I'll process next in the table list
004597
004598			 INITIALIZE ITEM-TABLE (IND).
004599
004600**** 
004601			 IF ARGUMENT = EDIT-VIEW 
004602			 MOVE SPACES TO DATI-LIB
004603			 STRING "            value:'" DELIMITED BY SIZE 
004604								ARGUMENT  DELIMITED BY "    "
004605							   "',selected:true, text:'" 
004606										  DELIMITED BY SIZE 
004607								BUFFER1   DELIMITED BY "    "
004608								"' "	  DELIMITED BY SIZE 
004609			 INTO DATI-LIB
004610			ELSE
004611			 STRING "            value:'" DELIMITED BY SIZE 
004612								ARGUMENT  DELIMITED BY "    "
004613							   "',text:'" DELIMITED BY SIZE 
004614								BUFFER1   DELIMITED BY "    "
004615								"' "	  DELIMITED BY SIZE 
004616			 INTO DATI-LIB.
004617
004618 EX-DO-ITEM.
004619		EXIT.
004620
004626
004627 DIVISORE.
004628
004629	    PERFORM VARYING IND4-WEB FROM LENGTH OF BUFFER   
004630			BY -1 UNTIL BUFFER(IND4-WEB:1) > SPACES
004640				OR IND4-WEB = ZEROS
004650				CONTINUE
004660		  END-PERFORM.
004670
004680		ADD 1 TO IND4-WEB.
004690
004700		MOVE "/DIVISORE/" TO BUFFER(IND4-WEB:).
004710
004720 EX-DIVISORE.
004730		EXIT.
004740
004750
004760 DO-TABLE.
004770
004780		MOVE 0 					TO INDEX-TABLE.
004790		MOVE 1					TO IND.
004800
004810 CICLO-TABLE.
004820
004830		 ADD  1 				TO INDEX-TABLE.
004840
004850
004860*** SI POSIZIONA SUL PRIMO CARATTERE  SIGNIFICATIVO
004870
004880		PERFORM VARYING IND FROM IND BY 1 UNTIL IND > LENGTH OF DATI
004890		 OR DATI(IND:1) > SPACES
004900		 CONTINUE
004910		END-PERFORM.
004920
004930		IF IND > LENGTH OF DATI 
004940		GO TO END-DO-TABLE.
004950
004960		MOVE 1					TO IND-TABLE
004970
004980		PERFORM VARYING IND FROM IND BY 1 UNTIL IND > LENGTH OF DATI
004990		 OR DATI(IND:1) = "="
005000
005010			MOVE DATI(IND:1)	TO NAME-TABLE( INDEX-TABLE) (IND-TABLE:1) 
005020			ADD 1				TO IND-TABLE
005030
005040		 CONTINUE
005050		END-PERFORM.
005060
005070		IF IND > LENGTH OF DATI 
005080		GO TO END-DO-TABLE.
005090
005100
005110		IF DATI(IND:1) = "="
005120		ADD 1		TO IND
005130		MOVE 1		TO IND-TABLE
005140
005150****  SI POSIZIONA SUL PRIMO '"' DOPO '=" esempio title="TESTO"  
005160*                                                       ! < qui
005170
005180		PERFORM VARYING IND FROM IND BY 1 UNTIL IND > LENGTH OF DATI
005190		 OR DATI(IND:1) = '"'
005200		 CONTINUE
005210		END-PERFORM
005220
005230		ADD 1				TO IND
005240
005250		PERFORM VARYING IND FROM IND BY 1 UNTIL IND > LENGTH OF DATI
005260		 OR DATI(IND:1) = '"'
005270
005280			MOVE DATI(IND:1)	TO VALUE-TABLE( INDEX-TABLE) (IND-TABLE:1) 
005290			ADD 1				TO IND-TABLE
005300
005310		 CONTINUE
005320		END-PERFORM
005330
005340****SOFWIRED update: size--> width:
005350
005360		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "SIZE"
005370		MOVE VALUE-TABLE(INDEX-TABLE)		TO ARGUMENT
005380		 MOVE SPACES TO VALUE-TABLE(INDEX-TABLE)
005390		STRING "width:" DELIMITED BY SIZE ARGUMENT DELIMITED BY "    " 
005400			";"	 DELIMITED BY SIZE INTO VALUE-TABLE(INDEX-TABLE)
005410		END-IF
005420
005430
005440		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "TYPE"
005450		MOVE VALUE-TABLE(INDEX-TABLE)		TO ARGUMENT
005460		 IF FUNCTION UPPER-CASE(ARGUMENT) = "MAIL"
005470		 MOVE "validType:'email' ," TO VALUE-TABLE(INDEX-TABLE)
005480		END-IF
005490		END-IF.
005500
005510
005520
005530
005540		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "TIMES"
005550			 MOVE VALUE-TABLE(INDEX-TABLE)		TO TIMES-TABLE(1:)
005560			 IF TIMES-TABLE(2:1) = SPACES
005570			 MOVE TIMES-TABLE(1:1)	TO TIMES-TABLE(2:1)
005580			 MOVE 0					TO TIMES-TABLE(1:1).
005590
005600***** CE' UN ELEMENTO DI MENU: DEVE CONTROLLARE SE ABILITATO O MENO
005610		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "DISABLED"
005620		 PERFORM OPEN-I-ABI 			THRU EX-OPEN-I-ABI 
005630
005640		 INITIALIZE ABILITAZIONE
005650
005660		 MOVE VALUE-TABLE(INDEX-TABLE)	TO MOD-ABI
005670		 MOVE 01					    TO ENTE-ABI
005680  	 IF GRUPPO-UTEN = ZEROS MOVE 1  TO GRUPPO-UTEN END-IF
005690		 MOVE GRUPPO-UTEN			    TO GRUPPO-ABI
005700		  
005710		 PERFORM LEGGO-ABI				THRU EX-LEGGO-ABI
005720
005730		 IF ESITO-NOK MOVE "true "		TO VALUE-TABLE(INDEX-TABLE)
005740
005750			IF NOME-UTEN = "ADMIN" 
005760		 		OR GRUPPO-UTEN = 99
005770				MOVE "false"			TO VALUE-TABLE(INDEX-TABLE)
005780			end-if
005790
005800		ELSE
005810		 MOVE "false"					TO VALUE-TABLE(INDEX-TABLE)
005820		 end-if
005830
005840		 CLOSE ARKABI.
005850
005860
005870
005880		PERFORM VARYING IND-WEB FROM LENGTH OF VALUE-TABLE(INDEX-TABLE) BY -1
005890		 UNTIL VALUE-TABLE(INDEX-TABLE) (IND-WEB:1) > SPACES
005891			OR IND-WEB = ZERO
005900		 CONTINUE
005910		END-PERFORM
005920
005930		ADD 1 TO IND-WEB
005940		MOVE "/DIVISORE/" TO VALUE-TABLE(INDEX-TABLE) (IND-WEB:).
005950		
005960
005970
005980		IF IND NOT > LENGTH OF DATI
005990		 ADD 1 					TO IND
006000		 GO TO CICLO-TABLE.
006010
006020 END-DO-TABLE.
006030
006040		SUBTRACT 1 FROM INDEX-TABLE
006050
006060		IF DEBUG-ON
006070
006080		MOVE 1					TO IND
006090
006100		PERFORM INDEX-TABLE TIMES
006110
006120		STRING 	"<!- " 	delimited by size
006130			 " Ind "  IND  " "
006140				NAME-TABLE(IND) DELIMITED BY "    "			
006150					 " contiene: " DELIMITED BY SIZE
006160				VALUE-TABLE(IND) DELIMITED BY "    "			
006170				" ->" 	delimited by SIZE INTO  COBW3-PUT-STRING
006180		PERFORM LINE-WEB THRU EX-LINE-WEB
006190
006200		ADD 1 TO IND
006210
006220		END-PERFORM.
006230
006240
006250 EX-DO-TABLE.
006260		EXIT. 
006270
006280**** DUMMY
006290 VIS-MESS. 
006300 EX-VIS-MESS.
006310		EXIT.
006320
006330
006340
006350**** DEVE ESISTERE UNA VARIABILE {menu} IN TABELLA
006360
006370 LETTURA-MOD.
006380
006390          INITIALIZE MENU.
006400
006410			PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > INDEX-TABLE
006420
006430			IF FUNCTION UPPER-CASE(NAME-TABLE(IND)) = "MENU"			
006440			  MOVE VALUE-TABLE(IND)		TO DATI-LIB(6:2)
006450			END-IF
006460				
006470			END-PERFORM.
006480
006490          MOVE DATI-LIB(6:2)      TO DEP-MOD.
006500
006510          PERFORM STARTO-MENU     THRU EX-STARTO-MENU.
006520          IF ESITO-NOK
006530          STRING  "MANCA MENU" MOD-MENU DELIMITED BY SIZE INTO MESSAGGIO
006540           PERFORM VIS-MESS    THRU EX-VIS-MESS
006550            GO TO EX-LETTURA-MOD.
006560
006570          SET FIRST-TIME  TO TRUE.
006580
006590 CICLO-LETTURA-MOD.
006600
006610          PERFORM LEGGO-NEXT-MENU THRU EX-LEGGO-NEXT-MENU.
006620
006630          IF FINE-FILE = "S" GO TO END-LETTURA-MOD.
006640
006650          IF MOD-MENU NOT = DEP-MOD GO CICLO-LETTURA-MOD.
006660
006670          IF ENT-MENU = SPACES GO TO CICLO-LETTURA-MOD.
006680
006690
006700          IF FUNCTION UPPER-CASE(DESC-MENU) = "SEPARATORE"
006710           MOVE '        <div class="menu-sep"></div>' TO DATI-WEB
006720           PERFORM PREPARA-LINE-WEB        THRU EX-PREPARA-LINE-WEB
006730            GO TO CICLO-LETTURA-MOD.
006740
006750          INITIALIZE ABILITAZIONE.
006760
006770          MOVE GRUPPO-UTEN        TO GRUPPO-ABI.
006780          IF GRUPPO-ABI = ZEROS
006790            MOVE 1                TO GRUPPO-ABI.
006800
006810          MOVE 01                 TO ENTE-ABI.
006820          MOVE CHIAVE-SEC-MENU    TO CHIAMATA-ABI.
006830
006840          PERFORM ABILITAZIONI    THRU EX-ABILITAZIONI.
006850
006860
006870          IF FUNZ-MENU = SPACES
006880              PERFORM MAKE-ENTITA   THRU EX-MAKE-ENTITA
006890           ELSE
006900              PERFORM MAKE-FUNZIONE THRU EX-MAKE-FUNZIONE.
006910
006920          GO TO CICLO-LETTURA-MOD.
006930
006940 END-LETTURA-MOD.
006950
006960          MOVE "          </div></div>"     TO DATI-WEB
006970          PERFORM PREPARA-LINE-WEB                  THRU EX-PREPARA-LINE-WEB.
006980
006990 EX-LETTURA-MOD.
007000          EXIT.
007010
007020 ABILITAZIONI.
007030
007040          PERFORM LEGGO-ABI       THRU EX-LEGGO-ABI.
007050
007060          IF ESITO-NOK
007070           SET DISABLED           TO TRUE.
007080
007090          IF NOME-UTEN = "ADMIN" OR GRUPPO-UTEN > 99
007100           MOVE "N"                TO FLAG-DISABLED.
007110
007120 EX-ABILITAZIONI.
007130          EXIT.
007140 MAKE-ENTITA.
007150
007160          IF NOT FIRST-TIME
007170          MOVE   "        </div></div>"     TO DATI-WEB
007180          PERFORM PREPARA-LINE-WEB        THRU EX-PREPARA-LINE-WEB
007190          ELSE
007200          MOVE "N"                        TO FLAG-TIME.
007210
007220
007230
007240          IF DISABLED
007250                  MOVE "disabled:true"            to wdisable
007260          else
007270                  MOVE "disabled:false"           to wdisable.
007280
007290          IF IMG-MENU > ZEROS
007300                  AND IMG-MENU < 20
007310                  MOVE WIMAGE(IMG-MENU)           TO WIMAGES
007320
007330              else
007340                  MOVE SPACES                     to wIMAGES.
007350
007360			MOVE SPACES TO DATI-WEB.
007370
007380
007390          STRING "             " DELIMITED BY SIZE
007400          '<div data-options="'
007410                  wdisable delimited by  "  "
007420                  wimages delimited by "   "
007430                   '"><span>' delimited BY  SIZE
007440          DESC-MENU DELIMITED BY "    " "</span><div>"
007450              DELIMITED BY SIZE INTO DATI-WEB.
007460
007470          PERFORM PREPARA-LINE-WEB        THRU EX-PREPARA-LINE-WEB.
007480
007490
007500 EX-MAKE-ENTITA.
007510          EXIT.
007520
007530
007540
007550 MAKE-FUNZIONE.
007560
007570
007580          IF DISABLED
007590          string
007600          '            <div data-options="disabled:true">'
007610          DESC-MENU DELIMITED BY "     " "</div>"
007620              DELIMITED BY SIZE INTO DATI-WEB
007630          ELSE
007640          STRING
007650          '            <div>'  DELIMITED BY SIZE
007660          DESC-MENU DELIMITED BY "     " "</div>"
007670              DELIMITED BY SIZE INTO DATI-WEB.
007680
007690          IF DISABLED
007700                  MOVE "disabled:true"            to wdisable
007710                  else
007720                  MOVE "disabled:false"           to wdisable.
007730
007740          IF IMG-MENU > ZEROS
007750                  AND IMG-MENU < 20
007760                  MOVE WIMAGE(IMG-MENU)           TO WIMAGES
007770
007780                  else
007790                  MOVE SPACES                     to wIMAGES.
007800
007810			MOVE SPACES TO DATI-WEB.
007820
007830          STRING "             " DELIMITED BY SIZE
007840                  '<div data-options="'
007850                  wdisable delimited by  "  "
007860                  wimages delimited by "   "
007870                  '"><a style="text-decoration:none;" href="' DELIMITED BY SIZE
007880                  PROG-MENU DELIMITED BY "   "
007890                  ".exe?MK-KEY=" SECTION-WEB DELIMITED BY SIZE
007900					"&MK-ENTITA=" ENT-MENU DELIMITED BY SIZE 
007910					"&MK-FUNZIONE=" FUNZ-MENU DELIMITED BY SIZE
007920                  '" class="easyui-menulink">' DELIMITED BY SIZE 
007930		            DESC-MENU DELIMITED BY "    " 
007940				    "</a></div>"
007950                  DELIMITED BY SIZE INTO DATI-WEB.
007960
007970          PERFORM PREPARA-LINE-WEB        THRU EX-PREPARA-LINE-WEB.
007980
007990
008000 EX-MAKE-FUNZIONE.
008010          EXIT.
008020
008030 PREPARA-LINE-WEB.
008040
008050			MOVE DATI-WEB					TO COBW3-PUT-STRING.
008060
008070		    PERFORM LINE-WEB 				THRU EX-LINE-WEB.
008080
008090 EX-PREPARA-LINE-WEB.
008100			EXIT.
