000010 @OPTIONS ALPHAL(WORD)
000020*
000030* Copyright (C) 2022 Federico Priolo TP ONE SRL federico.priolo@tp-one.it
000040*
000050* This program is free software; you can redistribute it and/or modify
000060* it under the terms of the GNU General Public License as published by
000070* the Free Software Foundation; either version 2, or (at your option)
000080* any later version.
000090*
000100* This program is distributed in the hope that it will be useful,
000110* but WITHOUT ANY WARRANTY; without even the implied warranty of
000120* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
000130* GNU General Public License for more details.
000140*
000150* You should have received a copy of the GNU General Public License
000160* along with this software; see the file COPYING.  If not, write to
000170* the Free Software Foundation, 51 Franklin Street, Fifth Floor
000180* Boston, MA 02110-1301 USA
000190*
000200
000210*
000220 identification division.
000230 PROGRAM-ID. MAKEFORM.
000240 AUTHOR. FEDERICO PRIOLO AGOSTO 2022
000250
000260*
000270* Copyright (C) 2010-2022 Federico Priolo TP ONE SRL federico.priolo@tp-one.it
000280*
000290* This program is free software; you can redistribute it and/or modify
000300* it under the terms of the GNU General Public License as published by
000310* the Free Software Foundation; either version 2, or (at your option)
000320* any later version.
000330*
000340* This program is distributed in the hope that it will be useful,
000350* but WITHOUT ANY WARRANTY; without even the implied warranty of
000360* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
000370* GNU General Public License for more details.
000380*
000390* You should have received a copy of the GNU General Public License
000400* along with this software; see the file COPYING.  If not, write to
000410* the Free Software Foundation, 51 Franklin Street, Fifth Floor
000420* Boston, MA 02110-1301 USA
000430*
000440 ENVIRONMENT DIVISION.
000450 CONFIGURATION    SECTION.
000460
000470          COPY "SPECIAL.CBL".
000480
000490 INPUT-OUTPUT SECTION.
000500 FILE-CONTROL.
000510
000520*
000530			COPY "SELWEB.CBL".
000540			COPY "SELMENU.CBL".
000550			COPY "SELABI.CBL".
000560			COPY "SELVIEW.CBL".
000570			COPY "SELUTEN.CBL".
000580			COPY "SELHTM.CBL".
000590                    
000600 DATA DIVISION.
000610 FILE SECTION.
000620
000630
000640			COPY "FDEWEB.CBL".
000650			COPY "FDEABI.CBL".
000660			COPY "FDEMENU.CBL".
000670			COPY "FDEVIEW.CBL".
000680			COPY "FDEUTEN.CBL".
000690			COPY "FDEHTM.CBL".
000700
000710		
000720
000730
000740
000750 WORKING-STORAGE SECTION.
000760
000770
000780          COPY "GLOBALS.CBL".
000790			COPY "IMAGES.CBL".
000800
000810
000820 77 FILENAME			PIC X(50).
000830 77 DATI  			PIC X(1024).
000840 77 NAME				PIC X(100).
000850
000860 77 SW-TABLE			PIC X.
000870   88 table-on		VALUE "Y".
000880   88 table-oFF		VALUE "N".
000890
000900 77 DEBUG				PIC X.
000910   88 debug-on		VALUE "Y".
000920   88 debug-oFF		VALUE "N".
000930
000940 77 FAST 				PIC X.
000950
000960 01 TAB-TABLE.		
000970		02 INDEX-TABLE 	PIC 9999.
000980		02 IND-TABLE  	PIC 9999.
000990		02 ITEM-TABLE OCCURS 20 TIMES.
001000		 09 NAME-TABLE	PIC X(30).
001010		 09 VALUE-TABLE	PIC X(512).
001020		02 TIMES-TABLE	PIC 99.
001030
001040 77 IND1				PIC 9(9).
001050 77 IND2				PIC 9(9).
001060 77 IND3				PIC 9(9).
001070 
001080  LINKAGE SECTION.
001090
001100 			COPY "COBW3.CBL".
001110
001120  PROCEDURE       DIVISION USING COBW3.
001130  DECLARATIVES.
001140  
001150  MANCA-FILE SECTION.
001160		USE AFTER STANDARD ERROR PROCEDURE ON ARKLIB ARKHTM.
001170  MANCAFILE.
001180   		CONTINUE.
001190  EX-MANCAFILE.
001200	    EXIT.
001210  END DECLARATIVES.
001220
001230
001240      PERFORM OPEN-I-MENU  	THRU EX-OPEN-I-MENU.
001250
001260		MOVE ENTITA-WEB			TO ENT-MENU
001270		MOVE FUNZIONE-WEB		TO FUNZ-MENU
001280		PERFORM LEGGO-TER-MENU	THRU EX-LEGGO-TER-MENU.
001290
001300		IF ESITO-NOK MOVE SPACES TO DESC-MENU.
001310
001320		CLOSE ARKMENU.
001330
001340      MOVE "MENU"             TO NOME-VIEW
001350		MOVE DESC-MENU			TO STRINGA-VIEW.
001360		PERFORM SCRITTURA-VIEW  THRU EX-SCRITTURA-VIEW.
001370
001380
001390
001400
001410
001420		MOVE "N"			TO SW-TABLE.
001430		MOVE "N"			TO DEBUG.
001440
001450		MOVE SPACES			TO FILE-WEB.
001460		MOVE ZEROS			TO IND-WEB.
001470
001480		INSPECT PAGE-WEB TALLYING IND-WEB FOR ALL "\"
001490		INSPECT PAGE-WEB TALLYING IND-WEB FOR ALL "/"
001500		IF IND-WEB = ZEROS
001510		STRING "TEMPLATE\" PAGE-WEB
001520		DELIMITED BY SIZE INTO FILE-WEB
001530		ELSE
001540		MOVE PAGE-WEB		TO FILE-WEB.
001550
001560      MOVE "PAGINA-HTM"       TO NOME-VIEW
001570		MOVE FILE-WEB 			TO STRINGA-VIEW.
001580		PERFORM SCRITTURA-VIEW  THRU EX-SCRITTURA-VIEW.
001590
001600
001610
001620		OPEN INPUT ARKWEB.
001630
001640		IF STATUS-WEB NOT = "00" 
001650         MOVE SPACES TO COBW3-PUT-STRING
001660         STRING "FILE TEMPLATE NON PRESENTE IN "  FILE-WEB
001670		   DELIMITED BY SIZE INTO  COBW3-PUT-STRING
001680		   PERFORM LINE-WEB THRU EX-LINE-WEB
001690          GO TO FINE.
001700
001710 CICLO.
001720		
001730		READ ARKWEB NEXT RECORD AT END GO TO FINE.
001740
001750		IF DATI-WEB = SPACES GO TO CICLO.
001760
001770***** asterisco commenta la riga
001780
001790		PERFORM VARYING IND FROM 1 BY 1 UNTIL DATI-WEB(IND:1) > SPACES
001800       CONTINUE
001810		END-PERFORM
001820
001830		IF DATI-WEB(IND:1) = "*" GO TO CICLO.
001840
001850		MOVE ZERO	TO IND.
001860
001870		INSPECT DATI-WEB TALLYING IND FOR ALL "("
001880		INSPECT DATI-WEB TALLYING IND FOR ALL ")"
001890
001900
001910		IF IND = 1
001920		STRING "mancano (/) " FILE-WEB DELIMITED BY "  "
001930		 " riga: " DATI-WEB 
001940		delimited by size into COBW3-PUT-STRING
001950		PERFORM LINE-WEB THRU EX-LINE-WEB
001960		 GO TO CICLO.
001970
001980
001990		IF IND NOT = 2 GO TO CICLO.
002000		
002010
002020
002030*** softwire switches= FAST(value="SI") scrive la pagina intera invece della singola riga
002040
002050		MOVE ZERO TO IND.
002060		INSPECT FUNCTION UPPER-CASE(DATI-WEB) TALLYING IND 
002070		 FOR ALL 'FAST(ENABLED="SI")'
002080		IF IND = 1
002090		MOVE "S"		TO  FAST
002100
002110		MOVE SPACES TO FILE-HTM
002120		ACCEPT TIME-SECTION-NOW FROM TIME
002130		STRING "C:\TEMP\" DELIMITED BY SIZE 
002140			SECTION-WEB DELIMITED BY "  "
002150			TIME-SECTION-NOW DELIMITED BY "  "
002160			".htm" delimited by size
002170			 INTO FILE-HTM
002180
002190		OPEN OUTPUT ARKHTM  
002200
002210		  IF STATUS-HTM NOT = "00"
002220		   MOVE SPACES TO FAST
002230         STRING "FILE HTM NON GENERATO "  FILE-HTM
002240		   DELIMITED BY SIZE INTO  COBW3-PUT-STRING
002250		   PERFORM LINE-WEB THRU EX-LINE-WEB
002260         GO TO FINE
002270	      END-IF
002280		
002290
002300		go to ciclo.
002310
002320
002330*** softwire switches= debug(value="SI")
002340
002350		MOVE ZERO TO IND.
002360		INSPECT FUNCTION UPPER-CASE(DATI-WEB) TALLYING IND 
002370		 FOR ALL 'DEBUG(ENABLED="SI")'
002380		IF IND = 1
002390		SET DEBUG-ON TO TRUE
002400		go to ciclo.
002410
002420		MOVE ZERO TO IND.
002430		INSPECT FUNCTION UPPER-CASE(DATI-WEB) TALLYING IND 
002440		 FOR ALL 'DEBUG(ENABLED="NO")'
002450		IF IND = 1
002460		SET DEBUG-OFF TO TRUE
002470		go to ciclo.
002480
002490
002500		
002510
002520
002530****  FILENAME nella forma AZIONE(PARAMETRI) DOVE:
002540*
002550*     AZIONE 		(il nome del template da aprire su template\AZIONE.HTM 
002560*     PARAMETRI 	(nella forma NOME="valore")
002570
002580		MOVE SPACES  TO FILENAME DATI INITIALIZE TAB-TABLE.
002590
002600		UNSTRING DATI-WEB DELIMITED BY "("
002610		 INTO FILENAME DATI.
002620
002630		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > LENGTH OF FILENAME
002640		 OR FILENAME(IND:1) > SPACES
002650		 CONTINUE
002660		END-PERFORM
002670	
002680		IF IND NOT > LENGTH OF FILENAME
002690		 MOVE FILENAME(IND:)	TO BUFFER
002700		 MOVE BUFFER			TO FILENAME.
002710
002720
002730**** SOFTWIRED= "HTML"
002740		
002750		IF FUNCTION UPPER-CASE(FILENAME) = "HTML"
002760		 MOVE DATI			TO COBW3-PUT-STRING
002770		 PERFORM VARYING IND FROM LENGTH OF COBW3-PUT-STRING BY -1
002780		 UNTIL IND = ZERO OR COBW3-PUT-STRING(IND:1) = ")"
002790	 	  CONTINUE
002800		 END-PERFORM
002810		 MOVE SPACES		TO COBW3-PUT-STRING(IND:1) 
002820		  PERFORM LINE-WEB THRU EX-LINE-WEB
002830			GO TO CICLO.
002840
002850
002860		 MOVE ZERO TO IND IND1
002870		 INSPECT DATI TALLYING IND FOR ALL "="
002880
002890		 IF IND NOT = ZEROS
002900		 MOVE ZERO TO IND
002910		 INSPECT DATI-WEB TALLYING IND FOR ALL QUOTE
002920		  DIVIDE IND BY  2 GIVING IND REMAINDER IND1
002930		
002940		  IF IND1 = ZEROS	
002950		  PERFORM DO-TABLE	THRU EX-DO-TABLE
002960		   ELSE
002970		   STRING "INDICARE UN NUMERO CORRETTO DI PARAMETRI CON GLI APICI"  DATI-WEB
002980		   DELIMITED BY SIZE INTO  COBW3-PUT-STRING
002990		   PERFORM LINE-WEB THRU EX-LINE-WEB
003000		  END-IF
003010		 END-IF.
003020				
003030
003040
003050
003060				
003070		IF TIMES-TABLE = ZERO MOVE 1 TO TIMES-TABLE.
003080
003090		PERFORM ELABORA-LIBRERIA	THRU EX-ELABORA-LIBRERIA 
003100			TIMES-TABLE  TIMES.
003110
003120		GO TO CICLO.
003130
003140 FINE.
003150
003160		CLOSE ARKWEB.
003170
003180
003190		IF FAST = "S"
003200		 CLOSE ARKHTM
003210	  	  MOVE FILE-HTM	TO COBW3-HTML-FILENAME
003220         CALL "COBW3_PUT_HTML" USING COBW3.
003230
003240
003250		EXIT PROGRAM.
003260
003270		COPY "PIOABI.CBL".
003280		COPY "PIOMENU.CBL".
003290	    COPY "PIOVIEW.CBL".
003300		
003310			
003320 ELABORA-LIBRERIA.
003321
003322**** eventuali librerie personalizzate
003330
003340		MOVE SPACES		TO FILE-LIB	
003350		STRING "lib\" DELIMITED BY SIZE 
003360			 FUNCTION LOWER-CASE(FILENAME) DELIMITED BY "   " 
003370				".htm" delimited by size into FILE-LIB.
003392
003393		OPEN INPUT ARKLIB.
003394
003395		IF STATUS-LIB = "35"
003396		MOVE SPACES		TO FILE-LIB	
003397		STRING "..\OPENPA\lib\" DELIMITED BY SIZE 
003398			 FUNCTION LOWER-CASE(FILENAME) DELIMITED BY "   " 
003399				".htm" delimited by size into FILE-LIB
003400		ELSE
003401		CLOSE ARKLIB.
003402		
003403
003404
003405		MOVE SPACES		TO COBW3-PUT-STRING
003410
003420		IF DEBUG-ON
003430		STRING 	"<!- " 	delimited by size
003440			 file-lib 	delimited by "   "
003450				"->" 	delimited by SIZE INTO  COBW3-PUT-STRING
003460		PERFORM LINE-WEB THRU EX-LINE-WEB.
003470
003480		OPEN INPUT ARKLIB.
003490	
003500		IF STATUS-LIB = "35"
003510		STRING "template: " FILE-LIB " NON PRESENTE"
003520		 delimited by SIZE INTO  COBW3-PUT-STRING
003530		PERFORM LINE-WEB THRU EX-LINE-WEB
003540		ELSE
003550		PERFORM TRATTA-LIB THRU EX-TRATTA-LIB
003560		CLOSE ARKLIB.
003570
003580 EX-ELABORA-LIBRERIA.
003590		EXIT.
003600
003610 LINE-WEB.
003620*
003630		IF COBW3-PUT-STRING = SPACES GO TO EX-LINE-WEB.
003640
003650		IF FAST = "S"
003660		 MOVE COBW3-PUT-STRING     TO REC-HTM
003670		 WRITE REC-HTM
003680		   ELSE
003690
003700		MOVE ZEROS          	TO     COBW3-PUT-STRING-LENGTH
003710      CALL  "COBW3_PUT_TEXT"  USING  COBW3.
003720
003730
003740		MOVE SPACES             TO COBW3-PUT-STRING.
003750
003760
003770 EX-LINE-WEB.
003780		EXIT.  
003790
003800 TRATTA-LIB.
003810		
003820		READ ARKLIB NEXT RECORD AT END GO TO EX-TRATTA-LIB.
003830
003840		IF DATI-LIB = SPACES GO TO TRATTA-LIB.
003850
003860*** DOPPIO {{  VUOL DIRE CHE NON DEVE ESSERE PROCESSATO COME VARIABILE MA SOLO COME ISTRUZIONE EASYUI E DIVENTA QUINDI {
003870
003880		MOVE ZERO TO IND.
003890		INSPECT DATI-LIB TALLYING IND FOR ALL "{{"
003900		IF IND NOT = ZEROS
003910		INSPECT DATI-LIB REPLACING ALL "{{" BY " {"
003920		INSPECT DATI-LIB REPLACING ALL "}}" BY "} "
003930	    GO TO RICICLO-LIB.
003940
003950
003960
003970		MOVE ZERO TO IND.
003980		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "<TABLE"
003990		IF IND = 1
004000		SET TABLE-ON TO TRUE.
004010
004020		MOVE ZERO TO IND.
004030		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "</TABLE"
004040		IF IND = 1
004050		SET TABLE-OFF TO TRUE.
004060
004070
004080		MOVE ZERO TO IND.
004090		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "</TD"
004100		IF IND = 1 AND TABLE-OFF GO TO TRATTA-LIB.
004110
004120		MOVE ZERO TO IND.
004130		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "<TD"
004140		IF IND = 1 AND TABLE-OFF GO TO TRATTA-LIB.
004150
004160
004170		MOVE ZERO TO IND.
004180		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "</TR"
004190		IF IND = 1 AND TABLE-OFF GO TO TRATTA-LIB.
004200
004210		MOVE ZERO TO IND.
004220		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "<TR"
004230		IF IND = 1 AND TABLE-OFF GO TO TRATTA-LIB.
004240
004250
004260
004270
004280***** asterisco commenta la riga
004290
004300		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > LENGTH OF DATI-LIB
004310			OR DATI-LIB(IND:1) > SPACES
004320		 CONTINUE
004330		END-PERFORM.
004340
004350		IF DATI-LIB(IND:1) = "*" 
004360
004370		 IF DEBUG-OFF
004380		  GO TO TRATTA-LIB
004390		  ELSE
004400		  STRING "<!- " 	delimited by size
004410		  dati-lib delimited by "   "
004420		    "->" 	delimited by SIZE INTO  COBW3-PUT-STRING
004430	      PERFORM LINE-WEB THRU EX-LINE-WEB
004440		  go to TRATTA-LIB
004450	 	 END-IF
004460	    END-IF.
004470
004480*
004490***  EVENTUALI {STRINGA} PRESENTI IN NAME-TABLE(1.....N) DA USARE PER COSTANTI TITOLO="PROVA"
004500*		 VANNO SOSTITUITI CON VALUE-TABLE(1....N)
004510
004520		PERFORM ESTRAI-STRINGA	THRU EX-ESTRAI-STRINGA.
004530
004540*
004550***  EVENTUALI <COBOL>VARIABILE</COBOL> PRESENTI 			DA USARE PER VARIABILI ALLOCATE SU ARKVIEW.
004560*		 VANNO SOSTITUITI CON RELATIVO VALORE..
004570
004580		PERFORM ESTRAI-COBOL	THRU EX-ESTRAI-COBOL.
004590
004600
004610		IF DATI-LIB = SPACES GO TO TRATTA-LIB.
004620
004630*
004640***  EVENTUALI @XXXXXYY SOFTWIRED INTERNAL PROCEDURE
004650*
004660
004670		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > LENGTH OF DATI-LIB
004680		 OR DATI-LIB(IND:1) > SPACES
004690		 CONTINUE
004700		END-PERFORM.
004710
004720
004730		IF FUNCTION UPPER-CASE(DATI-LIB(IND:5)) = "@MESI"
004740		 PERFORM DO-MESI		THRU EX-DO-MESI
004750		 GO TO TRATTA-LIB.
004760
004770
004780		IF FUNCTION UPPER-CASE(DATI-LIB(IND:5)) = "@LOAD"
004790       PERFORM OPEN-I-MENU	THRU EX-OPEN-I-MENU
004800		 PERFORM OPEN-I-ABI 	THRU EX-OPEN-I-ABI 
004810		 PERFORM LETTURA-MOD	THRU EX-LETTURA-MOD
004820		 CLOSE ARKMENU ARKABI
004830		  GO TO TRATTA-LIB.
004840
004850 RICICLO-LIB.
004860
004870		IF DATI-LIB > SPACES
004880		MOVE DATI-LIB			TO COBW3-PUT-STRING
004890		PERFORM LINE-WEB THRU EX-LINE-WEB.
004900
004910		GO TO TRATTA-LIB.
004920
004930 EX-TRATTA-LIB.
004940		EXIT.
004950
004960 ESTRAI-COBOL.
004970
004980		move zeros				TO ind.
004990
005000		INSPECT FUNCTION UPPER-CASE(DATI-LIB) TALLYING IND FOR ALL "<COBOL>"
005010
005020		IF IND = ZEROS  GO TO EX-ESTRAI-COBOL.
005030
005040
005050 CICLO-ESTRAI-COBOL.
005060
005070		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > LENGTH OF DATI-LIB
005080		 OR FUNCTION UPPER-CASE(DATI-LIB(IND:7)) = "<COBOL>"
005090		 CONTINUE
005100		END-PERFORM.
005110		
005120		MOVE IND   TO IND-WEB
005130
005140		SUBTRACT 1 FROM IND-WEB.
005150
005160		ADD 7 TO IND
005170
005180		MOVE ZERO TO SIZE-WEB
005190
005200		PERFORM VARYING IND1 FROM IND BY 1 UNTIL IND1 > LENGTH OF DATI-LIB
005210		 OR FUNCTION UPPER-CASE(DATI-LIB(IND1:8)) = "</COBOL>"
005220		 ADD 1 TO SIZE-WEB
005230		END-PERFORM
005240
005250
005260**** nome-VIEW= "LABEL" inserisce la DESC-VIEW della precedente lettura
005270
005280		IF DATI-LIB(IND:SIZE-WEB) = "LABEL"
005290  		MOVE DESC-VIEW			TO BUFFER
005300
005310			IF DESC-VIEW = SPACES
005320			MOVE "descrizione default" to BUFFER
005330			END-IF
005340
005350			MOVE SPACES 			TO DATI-LIB(IND1:8)
005360			ADD 8 					TO IND1
005370			GO TO END-ESTRAI-COBOL.
005380
005390
005400**** nome-VIEW= "CHECK" inserisce la parola checked o spazio per usare lo switchbutton
005410
005420		IF DATI-LIB(IND:SIZE-WEB) = "CHECK"
005430	
005440			INSPECT DATI-LIB REPLACING ALL '"<COBOL>CHECK</COBOL>"'
005450									    BY '                      '
005460			INSPECT DATI-LIB REPLACING ALL '"<cobol>CHECK</cobol>"'
005470									    BY '                      '
005480
005490  		MOVE EDIT-VIEW			TO BUFFER
005500
005510			IF BUFFER = "S"       
005520			MOVE "CHECKED"          to BUFFER
005530			ELSE
005540			MOVE SPACES             to BUFFER
005550			END-IF
005560
005570			MOVE SPACES 			TO DATI-LIB(IND1:8)
005580			ADD 8 					TO IND1
005590			GO TO END-ESTRAI-COBOL.
005600
005660
005670
005680		INITIALIZE VIEW.
005690
005700		MOVE DATI-LIB(IND:SIZE-WEB)  TO NOME-VIEW
005710
005720
005730**** ELIMINA </COBOL> dal buffer DATI-LIB
005740
005750		MOVE SPACES TO DATI-LIB(IND1:8)
005760
005770		ADD 8 			TO IND1.
005780
005790	
005800		PERFORM LEGGO-NOLOCK-VIEW	THRU EX-LEGGO-NOLOCK-VIEW.
005810
005820  	IF ESITO-NOK MOVE SPACES TO EDIT-VIEW. 
005830
005840		MOVE EDIT-VIEW				TO BUFFER.
005850
005860 END-ESTRAI-COBOL.
005870
005880		PERFORM DIVISORE			THRU EX-DIVISORE
005890
005900		MOVE BUFFER					TO ARGUMENT
005910
005920		MOVE DATI-LIB(1:IND-WEB)	TO BUFFER
005930
005940		PERFORM DIVISORE			THRU EX-DIVISORE
005950
005960		MOVE DATI-LIB(IND1:)		TO BUFFER1		
005970
005980		MOVE SPACES TO DATI-LIB
005990		 STRING BUFFER DELIMITED BY "/DIVISORE/"
006000			 ARGUMENT DELIMITED BY "/DIVISORE/"
006010			  BUFFER1 DELIMITED BY SIZE INTO DATI-LIB.
006020
006021**** GESTISCE PIU CAMPI NELLA STESSA RIGA...
006022
006023		GO TO ESTRAI-COBOL.
006024
006030 EX-ESTRAI-COBOL.
006040		EXIT.
006050
006060***** attenzione usare sempre coppie !!! se c'e uno script javascript va in loop...
006070
006080 ESTRAI-STRINGA.
006090
006100		move zeros				TO ind.
006110
006120		INSPECT DATI-LIB TALLYING IND FOR ALL "{"
006130		INSPECT DATI-LIB TALLYING IND FOR ALL "}"
006140
006150		IF IND < 2      GO TO EX-ESTRAI-STRINGA.
006160
006170 CICLO-ESTRAI-STRINGA.
006180
006190		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > LENGTH OF DATI-LIB
006200		 OR DATI-LIB(IND:1) = "{"
006210		 CONTINUE
006220		END-PERFORM.
006230
006240
006250		MOVE IND			TO IND1
006260		MOVE IND1			TO IND2
006270		ADD  1				TO IND2
006280		SUBTRACT 1 FROM IND1
006290
006300		MOVE SPACES			TO FIELD-WEB
006310		MOVE ZEROS 			TO IND3
006320
006330		PERFORM VARYING IND FROM IND2 BY 1 UNTIL IND2 > LENGTH OF DATI-LIB
006340		 OR DATI-LIB(IND:1) = "}"
006350
006360			 ADD 1				  TO IND3
006370			 MOVE DATI-LIB(IND:1) TO FIELD-WEB(IND3:1)
006380			
006390		END-PERFORM.
006400
006410		MOVE DATI-LIB(1:IND1)		TO BUFFER
006420
006430		PERFORM DIVISORE			THRU EX-DIVISORE
006440
006450
006460
006470		ADD 1						TO IND
006480		MOVE DATI-LIB(IND:)			TO BUFFER1		
006490
006500		PERFORM VARYING IND FROM 1 BY 1 UNTIL IND >  20 
006510		 OR FUNCTION UPPER-CASE(FIELD-WEB) = FUNCTION UPPER-CASE(NAME-TABLE(IND))
006520		CONTINUE
006530
006540		END-PERFORM.
006550
006560
006570
006580*** NON C'E
006590
006600		IF IND > 20
006610		 MOVE SPACES TO DATI-LIB
006620		 STRING BUFFER DELIMITED BY "/DIVISORE/"
006630			 " " BUFFER1 DELIMITED BY SIZE INTO DATI-LIB
006640
006650***** ELIMINA LA STRINGA non assegnata che rimane titolo=" "
006660			PERFORM VARYING IND-WEB FROM 1 BY 1 UNTIL IND-WEB > LENGTH OF DATI-LIB
006670
006680
006690			 IF IND-WEB < LENGTH OF DATI-LIB - 4
006700			 AND DATI-LIB(IND-WEB:4) = '=" "'
006710			 MOVE SPACES TO DATI-LIB(IND-WEB:4)
006720			 SUBTRACT 1 FROM IND-WEB
006730			 PERFORM VARYING IND-WEB FROM IND-WEB BY -1 UNTIL IND-WEB = ZERO			
006740			 OR DATI-LIB(IND-WEB:1) = SPACES
006750			 MOVE SPACES TO DATI-LIB(IND-WEB:1) 
006760			 END-PERFORM
006770			 END-IF
006780
006790			END-PERFORM
006800			 
006810		ELSE
006820
006830		 MOVE SPACES TO DATI-LIB
006840
006850* item-- combo formato VALORE, STRINGA COMBO ESEMPIO M, MASCHIO la variabile per il confronto con M e' in memoria n.b deve essere 
006860
006870		    IF FUNCTION UPPER-CASE(NAME-TABLE(IND)) = "ITEM"		 
006880		  	PERFORM DO-ITEM		THRU EX-DO-ITEM
006890		  	GO TO EX-ESTRAI-STRINGA
006900	     	END-IF
006910
006920* DEFAULT valore di default da assegnare ad un CHECK perche se non spuntato il programma web NON riporta nessun valore e lascerebbe il precedente
006930
006940
006950		    IF FUNCTION UPPER-CASE(NAME-TABLE(IND)) = "DEFAULT"		 
006960		  	PERFORM DO-DEFAULT	THRU EX-DO-DEFAULT
006970		  	GO TO EX-ESTRAI-STRINGA
006980	     	END-IF
006990
007000
007010
007020		IF FUNCTION UPPER-CASE(NAME-TABLE(IND)) = "NAME"
007030			MOVE VALUE-TABLE(IND) 		TO NAME
007040			INSPECT NAME REPLACING ALL "/DIVISORE/" 
007050									BY "          " 
007060			END-IF
007070
007080
007090
007100                                                                                                                       
007110		 STRING BUFFER DELIMITED BY "/DIVISORE/"
007120			 VALUE-TABLE(IND) DELIMITED BY "/DIVISORE/"
007130			  BUFFER1 DELIMITED BY SIZE INTO DATI-LIB.
007140
007150		 
007160		GO TO ESTRAI-STRINGA.
007170
007180 EX-ESTRAI-STRINGA.
007190      EXIT.
007200
007210 DO-DEFAULT.
007220
007230
007240		MOVE NAME				TO NOME-VIEW.
007250
007260  	READ ARKVIEW NO LOCK KEY IS CHIAVE-VIEW
007270    	 INVALID KEY  GO TO EX-DO-DEFAULT.
007280
007290
007300		MOVE VALUE-TABLE(IND)  	TO DEFAULT-VIEW.
007310
007320		INSPECT DEFAULT-VIEW REPLACING ALL "/DIVISORE/" 
007330									    BY "          " 
007340
007350
007360		MOVE "S"				TO USA-DEFAULT-VIEW.
007370
007380		PERFORM AGGIORNO-VIEW	THRU EX-AGGIORNO-VIEW
007390
007400		IF ESITO-NOK
007410			STRING "Errore rewrite " CHIAVE-VIEW 
007420		   	 DElimited by SIZE INTO  COBW3-PUT-STRING
007430			  PERFORM LINE-WEB THRU EX-LINE-WEB.
007440
007450
007460 EX-DO-DEFAULT.
007470		EXIT.
007480
007490 DO-MESI.
007500
007510		PERFORM VARYING MM-NORMALE FROM 1 BY 1 UNTIL MM-NORMALE > 12
007520
007530			MOVE SPACES		TO COBW3-PUT-STRING
007540
007550			IF MM-NORMALE = EDIT-VIEW(1:2)
007560			 MOVE " selected:true, " TO ARGUMENT
007570			ELSE
007580			 MOVE SPACES             TO ARGUMENT
007590			END-IF
007600
007610			STRING "{ value:'" DELIMITED BY SIZE
007620			    MM-NORMALE DELIMITED BY SIZE 
007630				"'," DELIMITED BY SIZE
007640				ARGUMENT DELIMITED BY "   "
007650				" text:'" DELIMITED BY SIZE
007660				MESE-IN-CORSO(MM-NORMALE) DELIMITED BY "   "
007670				"',iconCls:'icon-" DELIMITED BY SIZE
007680				FUNCTION LOWER-CASE(MESE-IN-CORSO(MM-NORMALE)) (1:3)
007690					"'},"
007700					 DELIMITED BY SIZE INTO COBW3-PUT-STRING
007710
007720			IF MM-NORMALE = 12
007730			 INSPECT COBW3-PUT-STRING REPLACING ALL "}," BY "} "
007740			END-IF
007750
007760
007770
007780		    PERFORM LINE-WEB 				THRU EX-LINE-WEB
007790
007800		END-PERFORM.
007810
007820
007830 EX-DO-MESI.
007840		EXIT.
007850
007860
007870**** VALUE-TABLE(IND) = "M, MASCHIO" > value:'M' , text:'MASCHIO'....
007880
007890 DO-ITEM.
007900
007910		   MOVE SPACES TO ARGUMENT BUFFER BUFFER1 DATI-LIB
007920		   UNSTRING VALUE-TABLE(IND) DELIMITED   ","
007930			 INTO ARGUMENT  BUFFER
007940			 UNSTRING BUFFER DELIMITED BY "/DIVISORE/"
007950			 INTO BUFFER1.
007960*** add a required comma ! be carefull 
007970
007980			INSPECT BUFFER1 REPLACING ALL "' iconCls:" 
007990								 BY  	  "',iconCls:" 
008000
008010** clear the item (so I'll process next in the table list
008020
008030			 INITIALIZE ITEM-TABLE (IND).
008040
008050**** 
008060			 IF ARGUMENT = EDIT-VIEW 
008070			 MOVE SPACES TO DATI-LIB
008080			 STRING "          { value:'" DELIMITED BY SIZE 
008090								ARGUMENT  DELIMITED BY "    "
008100							   "',selected:true, text:'" 
008110										  DELIMITED BY SIZE 
008120								BUFFER1   DELIMITED BY "    "
008130								"'},"	  DELIMITED BY SIZE 
008140			 INTO DATI-LIB
008150			ELSE
008160			 STRING "          { value:'" DELIMITED BY SIZE 
008170								ARGUMENT  DELIMITED BY "    "
008180							   "',text:'" DELIMITED BY SIZE 
008190								BUFFER1   DELIMITED BY "    "
008200								"'},"	  DELIMITED BY SIZE 
008210			 INTO DATI-LIB.
008220
008230			IF IND < INDEX-TABLE
008240			 AND FUNCTION UPPER-CASE(NAME-TABLE(IND + 1)) NOT = "ITEM"
008250			 INSPECT DATI-LIB REPLACING ALL "}," BY "} ".
008260
008270 EX-DO-ITEM.
008280		EXIT.
008290
008300 DIVISORE.
008310
008320	    PERFORM VARYING IND4-WEB FROM LENGTH OF BUFFER  BY -1 
008330			UNTIL IND4-WEB = ZEROS
008340			OR BUFFER(IND4-WEB:1) > SPACES
008350			CONTINUE
008360		  END-PERFORM.
008370
008380		ADD 1 TO IND4-WEB.
008390
008400		MOVE "/DIVISORE/" TO BUFFER(IND4-WEB:).
008410
008420 EX-DIVISORE.
008430		EXIT.
008440
008450
008460
008470 DO-TABLE.
008480
008490		MOVE 0 					TO INDEX-TABLE.
008500		MOVE 1					TO IND.
008510
008520 CICLO-TABLE.
008530
008540		 ADD  1 				TO INDEX-TABLE.
008550
008560
008570*** SI POSIZIONA SUL PRIMO CARATTERE  SIGNIFICATIVO
008580
008590		PERFORM VARYING IND FROM IND BY 1 UNTIL IND > LENGTH OF DATI
008600		 OR DATI(IND:1) > SPACES
008610		 CONTINUE
008620		END-PERFORM.
008630
008640		IF IND > LENGTH OF DATI 
008650		GO TO END-DO-TABLE.
008660
008670
008680		IF DATI(IND:) = ")"
008690		 GO TO END-DO-TABLE.
008700
008710
008720		MOVE 1					TO IND-TABLE
008730
008740		PERFORM VARYING IND FROM IND BY 1 UNTIL IND > LENGTH OF DATI
008750		 OR DATI(IND:1) = "="
008760		 OR DATI(IND:) = SPACES
008770
008780          IF IND-TABLE NOT  > LENGTH OF NAME-TABLE( INDEX-TABLE)
008790			MOVE DATI(IND:1)	TO NAME-TABLE( INDEX-TABLE) (IND-TABLE:1) 
008800			ADD 1				TO IND-TABLE
008810			END-IF
008820
008830		 CONTINUE
008840		END-PERFORM.
008850
008860		IF IND > LENGTH OF DATI 
008870		GO TO END-DO-TABLE.
008880
008890
008900		IF DATI(IND:1) = "="
008910		ADD 1		TO IND
008920		MOVE 1		TO IND-TABLE
008930
008940****  SI POSIZIONA SUL PRIMO '"' DOPO '=" esempio title="TESTO"  
008950*                                                       ! < qui
008960
008970		PERFORM VARYING IND FROM IND BY 1 UNTIL IND > LENGTH OF DATI
008980		 OR DATI(IND:1) = '"'
008990		 CONTINUE
009000		END-PERFORM
009010
009020		ADD 1				TO IND
009030
009040		PERFORM VARYING IND FROM IND BY 1 UNTIL IND > LENGTH OF DATI
009050		 OR DATI(IND:1) = '"'
009060		 OR DATI(IND:) = SPACES
009070
009080          IF IND-TABLE NOT  > LENGTH OF VALUE-TABLE( INDEX-TABLE)
009090			MOVE DATI(IND:1)	TO VALUE-TABLE( INDEX-TABLE) (IND-TABLE:1) 
009100			ADD 1				TO IND-TABLE
009110			END-IF
009120
009130		 CONTINUE
009140		END-PERFORM
009150
009160****SOFWIRED update: size--> width:
009170
009180		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "SIZE"
009190		MOVE VALUE-TABLE(INDEX-TABLE)		TO ARGUMENT
009200		 MOVE SPACES TO VALUE-TABLE(INDEX-TABLE)
009210		STRING "width:" DELIMITED BY SIZE ARGUMENT DELIMITED BY "    " 
009220			";"	 DELIMITED BY SIZE INTO VALUE-TABLE(INDEX-TABLE)
009230		END-IF
009240
009250
009260		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "CHAR"
009270		MOVE VALUE-TABLE(INDEX-TABLE)		TO ARGUMENT
009280		 MOVE SPACES TO VALUE-TABLE(INDEX-TABLE)
009290		STRING "validType:'length[0," DELIMITED BY SIZE ARGUMENT DELIMITED BY "    " 
009300			"]',"	 DELIMITED BY SIZE INTO VALUE-TABLE(INDEX-TABLE)
009310		END-IF
009320
009330
009340		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "TYPE"
009350		MOVE VALUE-TABLE(INDEX-TABLE)		TO ARGUMENT
009360		 IF FUNCTION UPPER-CASE(ARGUMENT) = "MAIL"
009370		 MOVE "validType:'email' ," TO VALUE-TABLE(INDEX-TABLE)
009380		END-IF
009390		END-IF.
009400
009410
009420
009430
009440		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "TIMES"
009450			 MOVE VALUE-TABLE(INDEX-TABLE)		TO TIMES-TABLE(1:)
009460			 IF TIMES-TABLE(2:1) = SPACES
009470			 MOVE TIMES-TABLE(1:1)	TO TIMES-TABLE(2:1)
009480			 MOVE 0					TO TIMES-TABLE(1:1).
009490
009500***** CE' UN ELEMENTO DI MENU: DEVE CONTROLLARE SE ABILITATO O MENO
009510		if FUNCTION UPPER-CASE(NAME-TABLE (INDEX-TABLE)) = "DISABLED"
009520		 PERFORM OPEN-I-ABI 			THRU EX-OPEN-I-ABI 
009530
009540		 INITIALIZE ABILITAZIONE
009550
009560		 MOVE VALUE-TABLE(INDEX-TABLE)	TO MOD-ABI
009570		 MOVE 01					    TO ENTE-ABI
009580  	 IF GRUPPO-UTEN = ZEROS MOVE 1  TO GRUPPO-UTEN END-IF
009590		 MOVE GRUPPO-UTEN			    TO GRUPPO-ABI
009600		  
009610		 PERFORM LEGGO-ABI				THRU EX-LEGGO-ABI
009620
009630		 IF ESITO-NOK MOVE "true "		TO VALUE-TABLE(INDEX-TABLE)
009640
009650			IF NOME-UTEN = "ADMIN" 
009660		 		OR GRUPPO-UTEN = 99
009670				MOVE "false"			TO VALUE-TABLE(INDEX-TABLE)
009680			end-if
009690
009700		ELSE
009710		 MOVE "false"					TO VALUE-TABLE(INDEX-TABLE)
009720		 end-if
009730
009740		 CLOSE ARKABI.
009750
009760
009770
009780		PERFORM VARYING IND-WEB FROM LENGTH OF VALUE-TABLE(INDEX-TABLE) BY -1
009790		 UNTIL VALUE-TABLE(INDEX-TABLE) (IND-WEB:1) > SPACES
009800			OR IND-WEB = ZERO
009810		 CONTINUE
009820		END-PERFORM
009830
009840		ADD 1 TO IND-WEB
009850		MOVE "/DIVISORE/" TO VALUE-TABLE(INDEX-TABLE) (IND-WEB:).
009860		
009870
009880
009890		IF IND NOT > LENGTH OF DATI
009900		 ADD 1 					TO IND
009910		 GO TO CICLO-TABLE.
009920
009930 END-DO-TABLE.
009940
009950		SUBTRACT 1 FROM INDEX-TABLE
009960
009970		IF DEBUG-ON
009980
009990		MOVE 1					TO IND
010000
010010		PERFORM INDEX-TABLE TIMES
010020
010030		STRING 	"<!- " 	delimited by size
010040			 " Ind "  IND  " "
010050				NAME-TABLE(IND) DELIMITED BY "    "			
010060					 " contiene: " DELIMITED BY SIZE
010070				VALUE-TABLE(IND) DELIMITED BY "    "			
010080				" ->" 	delimited by SIZE INTO  COBW3-PUT-STRING
010090		PERFORM LINE-WEB THRU EX-LINE-WEB
010100
010110		ADD 1 TO IND
010120
010130		END-PERFORM.
010140
010150
010160 EX-DO-TABLE.
010170		EXIT. 
010180
010190**** DUMMY
010200 VIS-MESS. 
010210 EX-VIS-MESS.
010220		EXIT.
010230
010240
010250
010260**** DEVE ESISTERE UNA VARIABILE {menu} IN TABELLA
010270
010280 LETTURA-MOD.
010290
010300          INITIALIZE MENU.
010310
010320			PERFORM VARYING IND FROM 1 BY 1 UNTIL IND > INDEX-TABLE
010330
010340			IF FUNCTION UPPER-CASE(NAME-TABLE(IND)) = "MENU"			
010350			  MOVE VALUE-TABLE(IND)		TO DATI-LIB(6:2)
010360			END-IF
010370				
010380			END-PERFORM.
010390
010400          MOVE DATI-LIB(6:2)      TO DEP-MOD.
010410
010420          PERFORM STARTO-MENU     THRU EX-STARTO-MENU.
010430          IF ESITO-NOK
010440          STRING  "MANCA MENU" MOD-MENU DELIMITED BY SIZE INTO MESSAGGIO
010450           PERFORM VIS-MESS    THRU EX-VIS-MESS
010460            GO TO EX-LETTURA-MOD.
010470
010480          SET FIRST-TIME  TO TRUE.
010490
010500 CICLO-LETTURA-MOD.
010510
010520          PERFORM LEGGO-NEXT-MENU THRU EX-LEGGO-NEXT-MENU.
010530
010540          IF FINE-FILE = "S" GO TO END-LETTURA-MOD.
010550
010560          IF MOD-MENU NOT = DEP-MOD GO CICLO-LETTURA-MOD.
010570
010580          IF ENT-MENU = SPACES GO TO CICLO-LETTURA-MOD.
010590
010600
010610          IF FUNCTION UPPER-CASE(DESC-MENU) = "SEPARATORE"
010620           MOVE '        <div class="menu-sep"></div>' TO DATI-WEB
010630           PERFORM PREPARA-LINE-WEB        THRU EX-PREPARA-LINE-WEB
010640            GO TO CICLO-LETTURA-MOD.
010650
010660          INITIALIZE ABILITAZIONE.
010670
010680          MOVE GRUPPO-UTEN        TO GRUPPO-ABI.
010690          IF GRUPPO-ABI = ZEROS
010700            MOVE 1                TO GRUPPO-ABI.
010710
010720          MOVE 01                 TO ENTE-ABI.
010730          MOVE CHIAVE-SEC-MENU    TO CHIAMATA-ABI.
010740
010750          PERFORM ABILITAZIONI    THRU EX-ABILITAZIONI.
010760
010770			IF DISABLED AND MOSTRA-MENU-UTEN = "N" 
010780			 GO TO CICLO-LETTURA-MOD.
010790
010800
010810          IF FUNZ-MENU = SPACES
010820              PERFORM MAKE-ENTITA   THRU EX-MAKE-ENTITA
010830           ELSE
010840              PERFORM MAKE-FUNZIONE THRU EX-MAKE-FUNZIONE.
010850
010860          GO TO CICLO-LETTURA-MOD.
010870
010880 END-LETTURA-MOD.
010890
010900          MOVE "          </div></div>"     TO DATI-WEB
010910          PERFORM PREPARA-LINE-WEB                  THRU EX-PREPARA-LINE-WEB.
010920
010930 EX-LETTURA-MOD.
010940          EXIT.
010950
010960 ABILITAZIONI.
010970
010980          MOVE "N"                TO FLAG-DISABLED.
010990
011000          PERFORM LEGGO-ABI       THRU EX-LEGGO-ABI.
011010
011020          IF ESITO-NOK
011030           SET DISABLED           TO TRUE.
011040
011050          IF NOME-UTEN = "ADMIN" OR GRUPPO-UTEN > 99
011060           MOVE "N"                TO FLAG-DISABLED.
011070
011080 EX-ABILITAZIONI.
011090          EXIT.
011100 MAKE-ENTITA.
011110
011120          IF NOT FIRST-TIME
011130          MOVE   "        </div></div>"     TO DATI-WEB
011140          PERFORM PREPARA-LINE-WEB        THRU EX-PREPARA-LINE-WEB
011150          ELSE
011160          MOVE "N"                        TO FLAG-TIME.
011170
011180
011190
011200          IF DISABLED
011210                  MOVE "disabled:true"            to wdisable
011220          else
011230                  MOVE "disabled:false"           to wdisable.
011240
011250          IF IMG-MENU > ZEROS
011260                  AND IMG-MENU < 20
011270                  MOVE WIMAGE(IMG-MENU)           TO WIMAGES
011280
011290              else
011300                  MOVE SPACES                     to wIMAGES.
011310
011320			MOVE SPACES TO DATI-WEB.
011330
011340
011350          STRING "             " DELIMITED BY SIZE
011360          '<div style="color:#0066cc" data-options="'
011370                  wdisable delimited by  "  "
011380                  wimages delimited by "   "
011390                   '"><span>' delimited BY  SIZE
011400          DESC-MENU DELIMITED BY "    " "</span><div>"
011410              DELIMITED BY SIZE INTO DATI-WEB.
011420
011430          PERFORM PREPARA-LINE-WEB        THRU EX-PREPARA-LINE-WEB.
011440
011450
011460 EX-MAKE-ENTITA.
011470          EXIT.
011480
011490
011500
011510 MAKE-FUNZIONE.
011520
011530
011540          IF DISABLED
011550          string
011560          '            <div data-options="disabled:true">'
011570          DESC-MENU DELIMITED BY "     " "</div>"
011580              DELIMITED BY SIZE INTO DATI-WEB
011590          ELSE
011600          STRING
011610          '            <div style="color:#0066cc">'  DELIMITED BY SIZE
011620          DESC-MENU DELIMITED BY "     " "</div>"
011630              DELIMITED BY SIZE INTO DATI-WEB.
011640
011650          IF DISABLED
011660                  MOVE "disabled:true"            to wdisable
011670                  else
011680                  MOVE "disabled:false"           to wdisable.
011690
011700          IF IMG-MENU > ZEROS
011710                  AND IMG-MENU < 20
011720                  MOVE WIMAGE(IMG-MENU)           TO WIMAGES
011730
011740                  else
011750                  MOVE SPACES                     to wIMAGES.
011760
011770			MOVE SPACES TO DATI-WEB.
011780
011790          STRING "             " DELIMITED BY SIZE
011800                  '<div data-options="'
011810                  wdisable delimited by  "  "
011820                  wimages delimited by "   "
011830                  '"><a style="text-decoration:none;" href="' DELIMITED BY SIZE
011840*                 DLL-MENU DELIMITED BY "   "
011850                  PROG-MENU DELIMITED BY "   "
011860                  ".exe?MK-KEY=" SECTION-WEB DELIMITED BY SIZE
011870					"&MK-ENTITA=" ENT-MENU DELIMITED BY SIZE 
011880					"&MK-FUNZIONE=" FUNZ-MENU DELIMITED BY SIZE
011890                  '" class="easyui-menulink">' DELIMITED BY SIZE 
011900		            DESC-MENU DELIMITED BY "    " 
011910				    "</a></div>"
011920                  DELIMITED BY SIZE INTO DATI-WEB.
011930
011940          PERFORM PREPARA-LINE-WEB        THRU EX-PREPARA-LINE-WEB.
011950
011960
011970 EX-MAKE-FUNZIONE.
011980          EXIT.
011990
012000 PREPARA-LINE-WEB.
012010
012020			MOVE DATI-WEB					TO COBW3-PUT-STRING.
012030
012040		    PERFORM LINE-WEB 				THRU EX-LINE-WEB.
012050
012060 EX-PREPARA-LINE-WEB.
012070			EXIT.
